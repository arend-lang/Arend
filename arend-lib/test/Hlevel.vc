\open ::Paths

\static \function
isContr (A : \Type0) => \Sigma (a : A) (\Pi (a' : A) -> a = a')

\static \function
I-isContr : isContr I => (left, \lam i => path (\lam j => squeeze i j))

\static \function
isProp (A : \Type0) => \Pi (a a' : A) -> a = a'

\static \function
isSet (A : \Type0) => \Pi (a a' : A) -> isProp (a = a')

\static \function
ofHlevel (n : Nat) (A : \Type0) : \Type0 <= \elim n
    | zero => isContr A
    | suc n => \Pi (a a' : A) -> ofHlevel n (a = a')

\static \function
ofHlevel1-isProp (A : \Type0) (f : ofHlevel (suc zero) A) : isProp A => \lam a a' => (f a a').1

\static \function
isContr-isProp (A : \Type0) (c : isContr A) : isProp A => \lam a a' => inv (c.2 a) *> c.2 a'

\static \function
isProp-ofHlevel1 (A : \Type0) (f : isProp A) : ofHlevel (suc zero) A => \lam a a' => (inv (f a a) *> f a a', Jl (\lam x q => inv (f a a) *> f a x = q) (inv-concat (f a a)))

\static \function
isProp-isSet (A : \Type0) (p : isProp A) : isSet A => \lam a a' => isContr-isProp (a = a') (isProp-ofHlevel1 A p a a')

\static \function
isProp-isProp (A : \Type0) : isProp (isProp A) => \lam f g => path (\lam i a a' => isProp-isSet A f a a' (f a a') (g a a') @ i)

\static \function
SigmaExt {A : \Type0} (B : A -> \Type0) (x y : \Sigma (a : A) (B a)) (p : x.1 = y.1) (q : transport B p x.2 = y.2) : x = y
    => path (\lam i => (p @ i, pathOver q @ i))

\static \function
isContrFun-isProp (A : \Type0) : isProp (isContr A) =>
    \lam c1 c2 => SigmaExt (\lam a => \Pi (a' : A) -> a = a') c1 c2 (c1.2 (c2.1))
    (path (\lam i a' => isProp-isSet A (isContr-isProp A c1) (c2.1) a'
        (transport (\lam a => \Pi (a' : A) -> a = a') (c1.2 (c2.1)) (c1.2) a') (c2.2 a') @ i))

\static \function
ofHlevel-isProp (n : Nat) (A : \Type0) : isProp (ofHlevel n A) <= \elim n
    | zero => isContrFun-isProp A
    | suc n => \lam f g => path (\lam i a a' => ofHlevel-isProp n (a = a') (f a a') (g a a') @ i)

\static \function
HProp => \Sigma (X : \Type0) (isProp X)
