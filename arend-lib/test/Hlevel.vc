\open ::Paths

\function
isContr {l : Level} (A : \Type l) => \Sigma (a : A) (\Pi (a' : A) -> a = a')

\function
I-isContr : isContr I => (left, \lam i => path (\lam j => squeeze i j))

\function
isProp {l : Level} (A : \Type l) => \Pi (a a' : A) -> a = a'

\function
isSet {l : Level} (A : \Type l) => \Pi (a a' : A) -> isProp (a = a')

\function
ofHlevel {l : Level} (n : Nat) (A : \Type l) : \Type l <= \elim n
    | zero => isContr A
    | suc n => \Pi (a a' : A) -> ofHlevel n (a = a')

\function
ofHlevel1-isProp {l : Level} (A : \Type l) (f : ofHlevel (suc zero) A) : isProp A => \lam a a' => (f a a').1

\function
isContr-isProp {l : Level} (A : \Type l) (c : isContr A) : isProp A => \lam a a' => inv (c.2 a) *> c.2 a'

\function
isProp-ofHlevel1 {l : Level} (A : \Type l) (f : isProp A) : ofHlevel (suc zero) A => \lam a a' => (inv (f a a) *> f a a', Jl (\lam x q => inv (f a a) *> f a x = q) (inv-concat (f a a)))

\function
isProp-isSet {l : Level} (A : \Type l) (p : isProp A) : isSet A => \lam a a' => isContr-isProp (a = a') (isProp-ofHlevel1 A p a a')

\function
isProp-isProp {l : Level} (A : \Type l) : isProp (isProp A) => \lam f g => path (\lam i a a' => isProp-isSet A f a a' (f a a') (g a a') @ i)

\function
SigmaExt {l : Level} {A : \Type l} (B : A -> \Type l) (x y : \Sigma (a : A) (B a)) (p : x.1 = y.1) (q : transport B p x.2 = y.2) : x = y
    => path (\lam i => (p @ i, pathOver q @ i))

\function
isContrFun-isProp {l : Level} (A : \Type l) : isProp (isContr A) =>
    \lam c1 c2 => SigmaExt (\lam a => \Pi (a' : A) -> a = a') c1 c2 (c1.2 (c2.1))
    (path (\lam i a' => isProp-isSet A (isContr-isProp A c1) (c2.1) a'
        (transport (\lam a => \Pi (a' : A) -> a = a') (c1.2 (c2.1)) (c1.2) a') (c2.2 a') @ i))

\function
ofHlevel-isProp {l : Level} (n : Nat) (A : \Type l) : isProp (ofHlevel n A) <= \elim n
    | zero => isContrFun-isProp A
    | suc n => \lam f g => path (\lam i a a' => ofHlevel-isProp n (a = a') (f a a') (g a a') @ i)


