\open HedbergTheorem
\open Paths
\open Data.Nat.Properties
\open Data.Either

\static \data Int 
	  | pos Nat
	  | neg Nat
	\with
	  | neg zero => pos zero

\static \function
zeroZ => pos zero

\static \function
sucZ (x : Int) : Int <= \elim x
   | pos n => pos (suc n)   
   | neg (suc n) => neg n

\static \function
predZ (x : Int) : Int <= \elim x
   | pos (suc n) => pos n
   | pos zero => neg (suc zero)
   | neg n => neg (suc n)

\static \function
absZ (x : Int) : Nat <= \elim x
   | pos n => n
   | neg n => n

{-
\static \function
zero-neqZ (n : Nat) : ((pos (suc n) != zeroZ) && (neg (suc n) != zeroZ)) => (\lam (p-eq : pos (suc n) = zeroZ) => zero-ne-suc (inv (pmap absZ p-eq)), \lam (p-eq : pos (suc n) = zeroZ) => zero-ne-suc (inv (pmap absZ p-eq)))
-}

\static \function 
sucZ-predZ-linv (x : Int) : (predZ (sucZ x) = x) <= \elim x
	| pos n => idpe (pos n)
	| neg (suc n) => idpe (neg (suc n))

\static \function 
sucZ-predZ-rinv (x : Int) : (sucZ (predZ x) = x) <= \elim x
	| pos (suc n) => idpe (pos (suc n))
	| neg n => idpe (neg n)
	| pos zero => idpe (pos zero)

--  (_ : decidable-eq (pos l) (predZ m))

\static \function
Int-decEq (n : Int) (m : Int) : (decidable-eq n m) <= 
\elim n, m
   | pos (suc l), m => (\case (Int-decEq (pos l) (predZ m))
     	      	    	      	    | inl p-eq => inl (pos (suc l) ==< pmap sucZ p-eq >== sucZ (predZ m) ==< sucZ-predZ-rinv m >== m !qed)
				    | inr p-neq => inr (\lam (p-eq : pos (suc l) = m) => p-neq (pos l ==< pmap predZ p-eq >== predZ m !qed)))
   | pos zero, pos zero => inl idp 
   | pos zero, pos (suc l) => inr (\lam (p-eq : pos zero = pos (suc l)) => zero-ne-suc (pmap absZ p-eq))				   
   | pos zero, neg (suc l) => inr (\lam (p-eq : pos zero = neg (suc l)) => zero-ne-suc (pmap absZ p-eq))	
   | neg (suc l), m => \case (Int-decEq (neg l) (sucZ m))
     	      	    	      	    | inl p-eq => inl (neg (suc l) ==< pmap predZ p-eq >== predZ (sucZ m) ==< sucZ-predZ-linv m >== m !qed)
				    | inr p-neq => inr (\lam (p-eq : neg (suc l) = m) => p-neq (neg l ==< pmap sucZ p-eq >== sucZ m !qed))

{- -}

\static \function
Int-isSet => Hedberg-thm (\lam (n m : Int) => Int-decEq n m)
