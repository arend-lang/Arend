\static \data List (A : \Type0)
    | nil
    | \infixr 5 (::) A (List A)

\static \function
singleton {A : \Type0} (a : A) => a :: nil

\static \function
headDefault {A : \Type0} (default : A) (xs : List A) : A <= \elim xs
    | nil => default
    | (::) x _ => x

\static \function
tail {A : \Type0} (xs : List A) : List A <= \elim xs
    | nil => nil
    | (::) _ xs => xs

\static \function \infixr 5
(++) {A : \Type0} (xs ys : List A) : List A <= \elim xs
    | nil => ys
    | (::) x xs => x :: xs ++ ys

\static \function
snoc {A : \Type0} (xs : List A) (x : A) => xs ++ singleton x

\open Data.Bool

\static \function
null {A : \Type0} (xs : List A) : Bool <= \elim xs
    | nil => true
    | _ => false

\static \function
map {A B : \Type0} (f : A -> B) (xs : List A) : List B <= \elim xs
    | nil => nil
    | (::) x xs => f x :: map f xs

\static \function
replicate {A : \Type0} (n : Nat) (a : A) : List A <= \elim n
    | zero => nil
    | suc n => a :: replicate n a

\static \function
zipWith {A B C : \Type0} (f : A -> B -> C) (xs : List A) (ys : List B) : List C <= \elim xs
    | nil => nil
    | (::) x xs <= \elim ys
        | nil => nil
        | (::) y ys => f x y :: zipWith f xs ys

\static \function
zip {A B : \Type0} => zipWith (\lam (x : A) (y : B) => (x, y))

\static \function
intersperse {A : \Type0} (x : A) (xs : List A) : List A <= \elim xs
    | nil => nil
    | (::) y ys <= \elim ys
        | nil => singleton y
        | _ => y :: x :: intersperse x ys

\static \function
foldr {A B : \Type0} (f : A -> B -> B) (b : B) (xs : List A) : B <= \elim xs
    | nil => b
    | (::) x xs => f x (foldr f b xs)

\static \function
foldl {A B : \Type0} (f : A -> B -> A) (xs : List B) (a : A) : A <= \elim xs
    | nil => a
    | (::) x xs => foldl f xs (f a x)

\static \function
concat {A : \Type0} : List (List A) -> List A => foldr (++) nil

\static \function
concatMap {A B : \Type0} (f : A -> List B) (xs : List A) => concat (map f xs)

\static \function sum => foldr Arith.(+) 0

\static \function product => foldr Arith.(*) 1

\static \function
length {A : \Type0} => foldr (\lam (_ : A) => suc) 0

\static \function
reverse {A : \Type0} (xs : List A) : List A => foldl (\lam xs x => x :: xs) xs nil

\static \function
take {A : \Type0} (n : Nat) (xs : List A) : List A <= \elim n
    | zero => nil
    | suc n <= \elim xs
        | nil => nil
        | (::) x xs => x :: take n xs

\static \function
drop {A : \Type0} (n : Nat) (xs : List A) : List A <= \elim n
    | zero => xs
    | suc n <= \elim xs
        | nil => nil
        | (::) x xs => drop n xs

\static \function
splitAt {A : \Type0} (n : Nat) (xs : List A) : \Sigma (List A) (List A) <= \elim n
    | zero => (nil, xs)
    | suc n <= \elim xs
        | nil => (nil, nil)
        | (::) x xs => \let p => splitAt n xs
                       \in (x :: p.1, p.2)

{-
\static \function
takeWhile {A : \Type0} (p : A -> Bool) (xs : List A) : List A <= \elim xs
    | nil => nil
    | (::) x xs => \case p x
        | true => x :: takeWhile p xs
        | false => nil

\static \function
dropWhile {A : \Type0} (p : A -> Bool) (xs : List A) : List A <= \elim xs
    | nil => nil
    | (::) x xs => \case p x
        | true => dropWhile p xs
        | false => x :: xs

\static \function
span {A : \Type0} (p : A -> Bool) (xs : List A) : \Sigma (List A) (List A) <= \elim xs
    | nil => (nil, nil)
    | (::) x xs => \case p x
        | true => \let s => span p xs
                  \in (x :: s.1, s.2)
        | false => (nil, x :: xs)

\static \function
break {A : \Type0} (p : A -> Bool) => span (\lam (a : A) => not (p a))
-}

\static \function
inits {A : \Type0} (xs : List A) : List (List A) <= \elim xs
    | nil => nil :: nil
    | (::) x xs => nil :: map ((::) x) (inits xs)

\static \function
tails {A : \Type0} (xs : List A) : List (List A) <= \elim xs
    | nil => nil :: nil
    | (::) x xs => (x :: xs) :: tails xs

\open Data.Maybe

\static \function
gfilter {A B : \Type0} (f : A -> Maybe B) (xs : List A) : List B <= \elim xs
    | nil => nil
    | (::) x xs => maybe (gfilter f xs) (\lam b => b :: gfilter f xs) (f x)

\static \function
filter {A : \Type0} (f : A -> Bool) => gfilter (\lam (a : A) => if (f a) (just a) nothing)

\static \function
partition {A : \Type0} (f : A -> Bool) (xs : List A) : \Sigma (List A) (List A) <= \elim xs
    | nil => (nil, nil)
    | (::) x xs => \let p => partition f xs
                   \in if (f x) (x :: p.1, p.2) (p.1, x :: p.2)
