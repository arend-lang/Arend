\import Algebra.Meta
\import Algebra.Monoid
\import Algebra.Pointed
\import Arith.Nat
\import Category
\import Data.List
\import Meta
\import Paths

\lemma test1 {M : Monoid} (x y z w : M)
  : (x * (ide * y)) * ((ide * z) * w) = x * ((y * (z * ide)) * w)
  => equation.monoid

\lemma test1a {M : Monoid} (x y z w : M) (p : x * (y * (z * w)) = w * (z * (y * x)))
  : (x * (ide * y)) * ((ide * z) * w) = w * ((z * (y * ide)) * x)
  => equation.monoid p

\lemma test1b {M : Monoid} (x y z w : M) (p : (x * (ide * y)) * ((ide * z) * w) = w * ((z * (y * ide)) * x))
  : x * (y * (z * w)) = w * (z * (y * x))
  => equation.monoid in p

\lemma testAdd1 {M : AddMonoid} (x y z w : M)
  : (x + (zro + y)) + ((zro + z) + w) = x + ((y + (z + zro)) + w)
  => equation.addMonoid

\lemma testAdd1a {M : AddMonoid} (x y z w : M) (p : x + (y + (z + w)) = w + (z + (y + x)))
  : (x + (zro + y)) + ((zro + z) + w) = w + ((z + (y + zro)) + x)
  => equation.addMonoid p

\lemma testAdd1b {M : AddMonoid} (x y z w : M) (p : (x + (zro + y)) + ((zro + z) + w) = w + ((z + (y + zro)) + x))
  : x + (y + (z + w)) = w + (z + (y + x))
  => equation.addMonoid in p


\lemma testComm1 {M : CMonoid} (x y z w : M)
  : (w * (ide * x)) * ((ide * y) * z) = x * ((y * (z * ide)) * w)
  => equation.cMonoid

\lemma testComm2 {M : CMonoid} (x y z w : M) (p : w * (x * (x * z)) = w * (x * (z * y)))
  : (w * (ide * x)) * ((ide * x) * z) = x * ((y * (z * ide)) * w)
  => equation.cMonoid p

\lemma testComm3 {M : CMonoid} (x y z w : M) (p : (w * (ide * x)) * ((ide * x) * z) = x * ((y * (z * ide)) * w))
  : w * (x * (x * z)) = w * (x * (z * y))
  => equation.cMonoid in p

\lemma testAb1 {M : AbMonoid} (x y z w : M)
  : (w + (zro + x)) + ((zro + y) + z) = x + ((y + (z + zro)) + w)
  => equation.abMonoid

\lemma testAb2 {M : AbMonoid} (x y z w : M) (p : w + (x + (x + z)) = w + (x + (z + y)))
  : (w + (zro + x)) + ((zro + x) + z) = x + ((y + (z + zro)) + w)
  => equation.abMonoid p

\lemma testAb3 {M : AbMonoid} (x y z w : M) (p : (w + (zro + x)) + ((zro + x) + z) = x + ((y + (z + zro)) + w))
  : w + (x + (x + z)) = w + (x + (z + y))
  => equation.abMonoid in p


\lemma testProof1 {M : Monoid} (x y : M) (p : x * y = ide) : (x * x) * (y * y) = ide
  => equation.monoid {pmap (x * __ * y) p, p}

\lemma testProof2 {M : AddMonoid} (x y : M) (p : x + y = zro) : (x + x) + (y + y) = zro
  => equation.addMonoid {pmap (x + __ + y) p} p

\lemma testProof3 {M : Monoid} (x y : M) (p : x * y = ide) : (x * x) * (y * y) = ide
  => equation.monoid {p,p}

\lemma testProof4 {M : Monoid} (x y : M) (p : x * y = ide) : (x * x) * (y * x) * y = x
  => equation.monoid {p}

\lemma testProofComm1 {M : CMonoid} (x y : M) (p : y * x = ide) : (y * x) * (y * x) = ide
  => equation.cMonoid {pmap (x * __ * y) p, p}

\lemma testProofComm2 {M : AbMonoid} (x y : M) (p : y + x = zro) : (y + x) + (y + x) = zro
  => equation.abMonoid {pmap (x + __ + y) p} p

\lemma testProofArg1 {M : Monoid} (x y z : M) (p : x * y = ide) (q : (x * x) * (y * y) = z) : x * y = z
  => equation.monoid {pmap (x * __ * y) p} in q

\lemma testProofArg2 {M : AddMonoid} (x y z : M) (p : x + y = zro) (q : (x + x) + (y + y) = z) : zro = z
  => equation.addMonoid {pmap (x + __ + y) p, p} in q


\lemma test3 {M : Monoid} (x y z : M) (p : x * z = z * x) (q : y * z = z * y)
  : (y * x) * (y * z) = (y * z) * (x * y)
  => equation

\lemma test3a {M : Monoid} (x y z : M) (p : x * z = z * x) (q : y * z = z * y)
  : (y * x) * (y * z) = (y * z) * (x * y)
  => equation {usingOnly (p,q)}

\lemma test3b {M : Monoid} (x y z : M) (p : x * z = z * x) (q : y * z = z * y)
  : (y * x) * (y * z) = (y * z) * (x * y)
  => equation {usingOnly (inv p, q)}

\lemma test4 {M : Monoid} (x y z : M) (p : z * x = x * z) (q : z * z = x * x)
  : z * (x * z) = x * (x * x) => equation

\lemma test4a {M : Monoid} (x y z : M) (p : z * x = x * z) (q : z * z = x * x)
  : z * (x * z) = x * (x * x)
  => equation {usingOnly (p,q)}

\lemma test5 {M : Monoid} (x y z : M) (p : (x * y) * z = z * y) (q : y * z = z * (y * x))
  : (x * z) * (y * (x * x)) = (z * y) * x
  => equation

\lemma test6 {M : Monoid} (x : M) (p : x * x = x * x)
  => fails {x * x = x} equation {usingOnly p}

\lemma test7 {M : Monoid} (x y : M) (p : x * y = x) : x * y * y = x
  => equation

\lemma test7a {M : Monoid} (x y : M) (p : x * y = x) : x = x * y * y
  => equation

\lemma test8 {M : Monoid} (x y z : M) (p : x * y = ide) (q : y * z = ide) : x = z
  => equation {inv ide-right *> pmap (x *) (inv q) *> inv *-assoc} (x * y * z)

\lemma test8a {M : Monoid} (x y z : M) (p : x * z = x) (q : z * z = y) (s : x * y = y) : x = y
  => equation (x * z * z) {*-assoc *> pmap (x *) q *> s}

\lemma testInstance (x y z : Nat) (p : x * y = z) : (z * x) * y = z * (1 * z)
  => equation {Monoid}

\lemma testListInstance {A : \Set} (x : A) (xs ys zs : List A) : zs ++ x :: xs ++ ys = (zs ++ x :: nil) ++ xs ++ ys
  => equation

\lemma addTest {M : AddMonoid} (x y : M) (p : x + y + 0 = x) : x = x + y + y
  => equation

\lemma catTest {C : Precat} {a b c d e : C} (p : Hom a b) (q : Hom b c) (r : Hom c d) (s : Hom d e)
  : (s ∘ r) ∘ (q ∘ p) = s ∘ (r ∘ q) ∘ (p ∘ id a)
  => equation

\lemma catTest2 {C : Precat} {a b c : C} (p : Hom a b) (q : Hom b a) (r : Hom a b) (s : Hom b c)
  : (s ∘ r) ∘ (q ∘ p) = s ∘ (r ∘ q) ∘ (p ∘ id a)
  => equation
