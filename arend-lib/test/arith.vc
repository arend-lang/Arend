\function
(+) (x y : Nat) : Nat
    <= \elim x | zero => y | suc x' => suc (x' + y)

\function
(*) (x y : Nat) : Nat
    <= \elim x | zero => zero | suc x' => y + (x' * y)

\function
idp {A : \Type0} {a : A} => path (\lam _ => a)

\function
pmap {A B : \Type0} (f : A -> B) {a a' : A} (p : Path (\lam _ => A) a a')
    => path (\lam i => f (p @ i))

\function
transport {A : \Type0} (B : A -> \Type0) {a a' : A} (p : Path (\lam _ => A) a a') (b : B a)
    => coe (\lam i => B (p @ i)) b right

\function
inv {A : \Type0} {a a' : A} (p : Path (\lam _ => A) a a')
    => transport (\lam a'' => Path (\lam _ => A) a'' a) p idp

\function
(*) {A : \Type0} {a a' a'' : A} (p : Path (\lam _ => A) a a') (q : Path (\lam _ => A) a' a'')
    => transport (\lam a''' => Path (\lam _ => A) a a''') q p

\function
plus-assoc (x y z : Nat) : Path (\lam _ => Nat) ((x + y) + z) (x + (y + z))
    <= \elim x | zero => path (\lam _ => y + z) | suc x' => path (\lam i => suc (plus-assoc x' y z @ i))

\function
plus-comm0 (y : Nat) : Path (\lam _ => Nat) y (y + zero)
    <= \elim y | zero => path (\lam _ => zero) | suc y' => path (\lam i => suc (plus-comm0 y' @ i))

\function
plus-comm (x y : Nat) : Path (\lam _ => Nat) (x + y) (y + x)
    <= \elim x | zero => plus-comm0 y | suc x' <= \elim y | zero => inv (plus-comm0 (suc x')) | suc y' => pmap suc (plus-comm x' (suc y') * pmap suc (inv (plus-comm x' y')) * plus-comm (suc x') y')
