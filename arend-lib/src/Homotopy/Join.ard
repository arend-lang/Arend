\import Equiv
\import Equiv.Univalence
\import Function
\import Homotopy.Cube
\import Homotopy.Pushout
\import Homotopy.Sphere
\import Homotopy.Suspension
\import Paths
\open Sphere(Sphere0,north0,south0)

\func Join (A B : \Type) => PushoutData {\Sigma A B} (\lam p => p.1) (\lam p => p.2)
  \where {
    \func jinl {A B : \Type} (a : A) : Join A B => pinl a

    \func jinr {A B : \Type} (b : B) : Join A B => pinr b

    \func jglue {A B : \Type} (a : A) (b : B) : jinl a = jinr b => path (pglue (a,b))
  }

\open Join

\func Join_Sphere0 (A : \Type) : QEquiv {Join Sphere0 A} {Susp A} \cowith
  | f => joinToSphere0
  | ret => sphere0ToJoin
  | ret_f => joinShere0Join
  | f_sec => sphere0JoinSphere0
  \where {
    \func joinToSphere0 {A : \Type} (j : Join Sphere0 A) : Susp A
      | pinl north0 => north
      | pinl south0 => south
      | pinr _ => south
      | pglue (north0,a) i => pglue a i
      | pglue (south0,_) _ => south

    \func sphere0ToJoin {A : \Type} (s : Susp A) : Join Sphere0 A
      | pinl _ => jinl north0
      | pinr _ => jinl south0
      | pglue a i => (jglue north0 a *> inv (jglue south0 a)) @ i

    \func joinShere0Join {A : \Type} (j : Join Sphere0 A) : sphere0ToJoin (joinToSphere0 j) = j
      | pinl north0 => idp
      | pinl south0 => idp
      | pinr a => jglue south0 a
      | pglue (north0,a) j => path (\lam i => Cube2.map
          (jglue north0 a *> inv (jglue south0 a))  -- top edge
          (jglue north0 a)                          -- bottom edge
          (idpe (jinl north0))                      -- left edge
          (jglue south0 a)                          -- right edge
          idp @ i @ j)
      | pglue (south0,a) j => path (\lam i => pglue (south0,a) (squeeze i j))

    \func sphere0JoinSphere0 {A : \Type} (s : Susp A) : joinToSphere0 (sphere0ToJoin s) = s
      | pinl _ => idp
      | pinr _ => idp
      | pglue a i =>
        \let t =>
          pmap joinToSphere0 (jglue north0 a *> inv (jglue south0 a)) ==< pmap_*>-comm joinToSphere0 (jglue north0 a) (inv (jglue south0 a)) >==
          pmerid a *> pmap joinToSphere0 (inv (jglue south0 a))       ==< pmap (pmerid a *>) (pmap_inv-comm joinToSphere0 (jglue south0 a)) >==
          pmerid a                                                    `qed
        \in path (\lam j => t @ j @ i)
  }

\func Join-assoc (A B C : \Type) : QEquiv {Join (Join A B) C} {Join A (Join B C)} \cowith
  | f => leftToRight
  | ret => rightToLeft
  | ret_f => leftRightLeft
  | f_sec => rightLeftRight
  \where {
    \func leftToRight {A B C : \Type} (j : Join (Join A B) C) : Join A (Join B C)
      | pinl (pinl a) => jinl a
      | pinl (pinr b) => jinr (jinl b)
      | pinl (pglue (a,b) j) => pglue (a, jinl b) j
      | pinr c => jinr (jinr c)
      | pglue (pinl a, c) i => pglue (a, jinr c) i
      | pglue (pinr b, c) i => jinr (pglue (b,c) i)
      | pglue (pglue (a,b) j, c) i =>
        Jl (\lam x p => Cube2 (jglue a x) (pmap jinr p) (jglue a (jinl b)) (idpe (jinr x)))
           (path (\lam j => path (\lam i => pglue (a, jinl b) (squeezeR j i))))
           (jglue b c)
        @ j @ i

    \func rightToLeft {A B C : \Type} (j : Join A (Join B C)) : Join (Join A B) C
      | pinl a => jinl (jinl a)
      | pinr (pinl b) => jinl (jinr b)
      | pinr (pinr c) => jinr c
      | pinr (pglue (b,c) j) => pglue (jinr b, c) j
      | pglue (a, pinl b) i => jinl (pglue (a,b) i)
      | pglue (a, pinr c) i => pglue (jinl a, c) i
      | pglue (a, pglue (b,c) j) i =>
        Jl (\lam x p => Cube2 (pmap jinl p) (jglue (jinl a) c) (idpe (jinl (jinl a))) (jglue x c))
           (path (\lam j => path (\lam i => pglue (jinl a, c) (squeeze j i))))
           (jglue a b)
        @ j @ i

    \func leftRightLeft {A B C : \Type} (j : Join (Join A B) C) : rightToLeft (leftToRight j) = j
      | pinl (pinl a) => idp
      | pinl (pinr b) => idp
      | pinl (pglue (a,b) j) => idp
      | pinr c => idp
      | pglue (pinl a, c) i => idp
      | pglue (pinr b, c) i => idp
      | pglue (pglue (a,b) j, c) i =>
        \let | s1 {y} (q : jinl b = y) : Cube2 (pmap rightToLeft (jglue a y))
                                               (pmap (\lam t => rightToLeft (jinr t)) q)
                                               (pmap jinl (jglue a b))
                                               (idpe (rightToLeft (jinr y)))
                 => path (\lam j => path (\lam i =>
                     rightToLeft (Jl (\lam x p => Cube2 (jglue a x) (pmap jinr p) (jglue a (jinl b)) (idpe (jinr x)))
                                      (path (\lam j => path (\lam i => pglue (a, jinl b) (squeezeR j i))))
                                      q @ j @ i)))
             | s2 {y} (q : jinl b = y) =>
                 Jl (\lam x p => Cube2 (pmap rightToLeft (jglue a x))
                                       (pmap (\lam t => rightToLeft (jinr t)) p)
                                       (pmap jinl (jglue a b))
                                       (idpe (rightToLeft (jinr x))))
                    (path (\lam j => path (\lam i => jinl (pglue (a,b) (squeezeR j i)))))
                    q
             | proof => coe (\lam n =>
                 coe (\lam k => Cube2 (coe (\lam l => Cube2 (path (\lam m => jinl (pglue (a,b) (squeeze (squeeze l m) n))))
                                                            (jglue (jinl a) c)
                                                            (idpe (jinl (jinl a)))
                                                            (jglue (pglue (a,b) (squeeze l n)) c))
                                           (path (\lam j => path (\lam i => pglue (jinl a, c) (squeeze j i))))
                                           right @ k)
                                      (path (\lam j => pglue (pglue (a,b) n, c) (squeeze k j)))
                                      (path (\lam m => jinl (pglue (a,b) (squeeze m n))))
                                      (idpe (pglue (pglue (a,b) n, c) k)))
                     (path (\lam j => path (\lam i => jinl (pglue (a,b) (squeeze (squeezeR j i) n)))))
                     right
                 = path (\lam j => path (\lam i => pglue (pglue (a,b) (squeeze j n), c) i))
               ) (Jl (\lam y q => Jl (\lam x p => Cube2 p p idp idp) idp q = idp) idp (jglue (jinl a) c)) right
        \in path (\lam k => (Jl (\lam y q => s1 q = s2 q) idp (jglue b c) *> proof) @ k @ j @ i)

    \func rightLeftRight {A B C : \Type} (j : Join A (Join B C)) : leftToRight (rightToLeft j) = j
      | pinl a => idp
      | pinr (pinl b) => idp
      | pinr (pinr c) => idp
      | pinr (pglue (b,c) j) => idp
      | pglue (a, pinl b) i => idp
      | pglue (a, pinr c) i => idp
      | pglue (a, pglue (b,c) j) i =>
        \let | s1 {y} (q : jinl a = y) : Cube2 (pmap (\lam t => leftToRight (jinl t)) q)
                                               (jglue a (jinr c))
                                               (idpe (jinl a))
                                               (pmap leftToRight (jglue y c))
                 => path (\lam j => path (\lam i =>
                     leftToRight (Jl (\lam x p => Cube2 (pmap jinl p) (jglue (jinl a) c) (idpe (jinl (jinl a))) (jglue x c))
                                     (path (\lam i => path (\lam j => pglue (jinl a, c) (squeeze i j))))
                                     q
                                  @ j @ i)))
             | s2 {y} (q : jinl a = y) =>
                 Jl (\lam x p => Cube2 (pmap (\lam t => leftToRight (jinl t)) p) (jglue a (jinr c)) (idpe (jinl a)) (pmap leftToRight (jglue x c)))
                    (path (\lam j => path (\lam i => pglue (a, jinr c) (squeeze j i))))
                    q
             | proof =>
                 Jl (\lam y q => coe (\lam k => Cube2 (path (\lam i => pglue (a, jinl b) (squeeze k i)))
                                                      (jglue a y)
                                                      (idpe (jinl a))
                                                      (Jl (\lam x p => Cube2 (jglue a x) (pmap jinr p) (jglue a (jinl b)) (idpe (jinr x)))
                                                          (path (\lam j => path (\lam i => pglue (a, jinl b) (squeezeR j i))))
                                                          q
                                                       @ k))
                                     (path (\lam j => path (\lam i => pglue (a,y) (squeeze j i))))
                                     right
                                 = path (\lam j => path (\lam i => pglue (a, q @ j) i)))
                    (Jl (\lam y q => coe (\lam k => Cube2 (path (\lam i => q @ squeeze k i))
                                                          q
                                                          (idpe (jinl a))
                                                          (path (\lam i => q @ squeezeR k i)))
                                         (path (\lam j => path (\lam i => q @ squeeze j i)))
                                         right
                                     = idpe q)
                        idp
                        (jglue a (jinl b)))
                    (jglue b c)
        \in path (\lam k => (Jl (\lam y q => s1 q = s2 q) idp (jglue a b) *> proof) @ k @ j @ i)
  }

\func Join_Susp (A B : \Type) : Join (Susp A) B = Susp (Join A B) =>
  Join (Susp A) B         ==< pmap (\lam X => Join X B) (QEquiv-to-= (symQEquiv (Join_Sphere0 A))) >==
  Join (Join Sphere0 A) B ==< QEquiv-to-= (Join-assoc Sphere0 A B) >==
  Join Sphere0 (Join A B) ==< QEquiv-to-= (Join_Sphere0 (Join A B)) >==
  Susp (Join A B)         `qed

\func Join_Sphere (A : \Type) (n : Nat) : Join (Sphere n) A = iterr Susp (suc n) A \elim n
  | 0 => QEquiv-to-= (Join_Sphere0 A)
  | suc n => Join_Susp (Sphere n) A *> pmap Susp (Join_Sphere A n)