\import Paths
\import Equiv
\import Equiv.Path

\func univalence {X Y : \Type} : QEquiv {X = Y} {Equiv {X} {Y}}
  => pathEquiv (\lam X Y => Equiv {X} {Y}) (\lam {A} {B} =>
      \let | =-to-Equiv {B : \Type} (p : A = B) => transport (\lam X => Equiv {A} {X}) p idEquiv
           | Equiv-to-= (e : Equiv {A} {B}) => \let qe : QEquiv => e \in path (iso qe.f qe.ret qe.ret_f qe.f_sec)
      \in \new Retraction {
        | f => =-to-Equiv
        | sec => Equiv-to-=
        | f_sec e => Equiv.equals (Jl (\lam _ p => Equiv.f {=-to-Equiv p} = (\lam a => coe (\lam i => p @ i) a right)) idp (Equiv-to-= e))
        })
