\import Data.Bool
\import Equiv
\import Equiv.Path
\import Equiv.Univalence
\import Logic
\import HLevel
\import Paths

\data Dec (E : \Type)
  | yes E
  | no (Not E)
  \where {
    \use \level Dec-isProp {E : \Prop} (d1 d2 : Dec E) : d1 = d2
      | yes e1, yes e2 => pmap yes (Path.inProp e1 e2)
      | yes e1, no ne2 => absurd (ne2 e1)
      | no ne1, yes e2 => absurd (ne1 e2)
      | no ne1, no ne2 => path (\lam i => no (\lam x => (absurd (ne1 x) : ne1 x = ne2 x) @ i))
  }

\func decToBool {E : \Type} (d : Dec E) : Bool
  | yes _ => true
  | no _ => false

\class Decide (E : \Type)
  | decide : Dec E

\func DecEq (A : \Type) => \Pi {a a' : A} -> Dec (a = a')

\class DecideEq (E : \Type)
  | decideEq : DecEq E

\func \infix 4 == {d : DecideEq} (x y : d.E) => decToBool (d.decideEq {x} {y})

\func Hedberg {A : \Type} (d : DecEq A) : isSet A => \lam x x' =>
  \let | R {a a' : A} (p : Dec (a = a')) : \Prop => \case p \with { | yes _ => Unit | no _ => Empty }
       | R' (a a' : A) => R {a} {a'} (d {a} {a'})
       | refl {a : A} (p : Dec (a = a)) => \case p \as p' \return R p' \with { | yes _ => unit | no q => q idp }
       | equiv : Equiv {x = x'} {R' x x'} => propPathEquiv R' (\lam a => refl (d {a} {a})) (\lam {a} {a'} s => \case d {a} {a'} \as p, s : R p \with {
         | yes p, _ => p
         | no _, s' => absurd s'
       }) {x} {x'}
  \in transport isProp (inv (univalence.ret equiv)) Path.inProp
  \where
    \func propPathEquiv {A : \Type} (R : A -> A -> \Prop) (refl : \Pi (a : A) -> R a a) (p : \Pi {a a' : A} -> R a a' -> a = a') {a a' : A} : QEquiv {a = a'} {R a a'}
      => pathEquiv R (\lam {a} {a'} => \new Retraction (\lam q => transport (R a) q (refl a)) p (\lam _ => Path.inProp _ _))
