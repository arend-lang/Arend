\import Algebra.Pointed
\import Category
\import Paths
\import Set.Category

\record PointedHom \extends SetHom {
  \override Dom : Pointed
  \override Cod : Pointed
  | func-ide : func ide = ide
} \where
    \func equals {X Y : Pointed} {f g : PointedHom X Y} (p : \Pi (x : X) -> f x = g x) : f = g
      => path (\lam i => \new PointedHom X Y (p __ @ i) (pathInProp (p ide @ __ = ide) f.func-ide g.func-ide @ i))

\instance PointedSetCat : Cat Pointed
  | Hom X Y => PointedHom X Y
  | id => id
  | o {X Y Z : Pointed} (g : PointedHom Y Z) (f : PointedHom X Y) => \new PointedHom {
    | func x => g (f x)
    | func-ide => pmap g f.func-ide *> g.func-ide
  }
  | id-left _ => idp
  | id-right _ => idp
  | o-assoc _ _ _ => idp
  | univalence (e : Iso) =>
    \let X=Y => Pointed.equls (path (iso e.f e.inv (\lam x => path (\lam i => func {e.f_inv @ i} x)) (\lam y => path (\lam i => func {e.inv_f @ i} y)))) func-ide
    \in (X=Y, PointedHom.equals (\lam x => Jl {Pointed} (\lam Z X=Z => func {transport (PointedHom e.dom __) X=Z (id e.dom)} x = coe (X=Z @ __) x right) idp X=Y))
  \where
    \func id (X : Pointed) : PointedHom X X \cowith
      | func x => x
      | func-ide => idp
