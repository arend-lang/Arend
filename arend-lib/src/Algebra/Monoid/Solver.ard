\import Algebra.Monoid
\import Algebra.Pointed
\import Arith.Nat
\import Data.List
\import Function.Meta
\import Meta
\import Order.Lattice
\import Order.PartialOrder
\import Paths
\import Paths.Meta
\import Set \hiding (#)

\data MonoidTerm (V : \Type)
  | var V
  | :ide
  | \infixl 7 :* (t s : MonoidTerm V)

\func normalize {V : \Type} (t : MonoidTerm V) => aux t nil
  \where
    \func aux {V : \Type} (t : MonoidTerm V) (acc : List V) : List V \elim t
      | var v => v :: acc
      | :ide => acc
      | :* t s => aux t (aux s acc)

\class Data \noclassifying {M : Monoid} {V : \Set} (f : V -> M) {
  \func interpret (t : MonoidTerm V) : M \elim t
    | var x => f x
    | :ide => ide
    | :* t s => interpret t * interpret s

  \func interpretNF (l : List V) : M \elim l
    | nil => ide
    | :: x nil => f x
    | :: x l => f x * interpretNF l
    \where
      \lemma cons (x : V) (l : List V) : interpretNF (x :: l) = f x * interpretNF l \elim l
        | nil => inv ide-right
        | :: a l => idp

  \lemma normalize-consistent (t : MonoidTerm V) : interpret t = interpretNF (normalize t)
    => inv ide-right *> aux t nil
    \where
      \lemma aux (t : MonoidTerm V) (acc : List V) : interpret t * interpretNF acc = interpretNF (normalize.aux t acc) \elim t
        | var x => inv (interpretNF.cons x acc)
        | :ide => ide-left
        | :* t s => *-assoc *> pmap (interpret t *) (aux s acc) *> aux t (normalize.aux s acc)

  \lemma terms-equality (t s : MonoidTerm V) (p : interpretNF (normalize t) = interpretNF (normalize s)) : interpret t = interpret s
    => normalize-consistent t *> p *> inv (normalize-consistent s)

  \lemma terms-equality-conv (t s : MonoidTerm V) (p : interpret t = interpret s) : interpretNF (normalize t) = interpretNF (normalize s)
    => inv (normalize-consistent t) *> p *> normalize-consistent s

  \lemma interpretNF_++ (l1 l2 : List V) : interpretNF (l1 ++ l2) = interpretNF l1 * interpretNF l2 \elim l1
    | nil => inv ide-left
    | :: a l1 => repeat {2} (rewrite interpretNF.cons) $ rewrite interpretNF_++ (inv *-assoc)

  \lemma replace-consistent (l : List V) (i s : Nat) (r : List V) (p : interpretNF (slice l i s) = interpretNF r)
    : interpretNF l = interpretNF (replace l i s r)
    => run {
      rewriteI {1} (slice.appendLem l i s),
      repeat {4} (rewrite interpretNF_++),
      rewriteI p,
      idp
    }
}

\class CData \extends Data {
  | V => Nat
  \override M : CMonoid

  \lemma sort-consistent (l : List Nat) : interpretNF l = interpretNF (RedBlack.sort l)
    => rewrite RedBlack.sort=insert (perm-consistent (Insertion.sort-perm l))

  \lemma perm-consistent {l l' : List Nat} (p : Perm l l') : interpretNF l = interpretNF l' \elim l, l', p
    | nil, nil, perm-nil => idp
    | :: a1 l1, :: a2 l2, perm-:: idp p => repeat {2} (rewrite interpretNF.cons) (pmap (f a1 *) (perm-consistent p))
    | :: a1 (:: a1' l1), :: a2 (:: a2' l2), perm-swap idp idp idp => repeat {2} (rewrite interpretNF.cons) (inv *-assoc *> (pmap (`* interpretNF l1) *-comm) *> *-assoc)
    | l1, l2, perm-trans p1 p2 => perm-consistent p1 *> perm-consistent p2

  \lemma normalize-consistent (t : MonoidTerm V) : interpret t = interpretNF (RedBlack.sort (normalize t))
    => Data.normalize-consistent t *> sort-consistent (normalize t)

  \lemma terms-equality (t s : MonoidTerm V) (p : interpretNF (RedBlack.sort (normalize t)) = interpretNF (RedBlack.sort (normalize s))) : interpret t = interpret s
    => normalize-consistent t *> p *> inv (normalize-consistent s)

  \lemma terms-equality-conv (t s : MonoidTerm V) (p : interpret t = interpret s) : interpretNF (RedBlack.sort (normalize t)) = interpretNF (RedBlack.sort (normalize s))
    => inv (normalize-consistent t) *> p *> normalize-consistent s

} \where {
  \open Data
  \open Sort
}

\class LData {L : Bounded.MeetSemilattice} \extends CData {
  | M => L

  \func removeDuplicates (l : List Nat) : List Nat \elim l
    | nil => nil
    | :: a nil => a :: nil
    | :: a (:: a' _ \as l) => \case decideEq a a' \with {
      | yes _ => removeDuplicates l
      | no _ =>  a :: removeDuplicates l
    }

  \lemma removeDuplicates-consistent (l : List Nat) : interpretNF l = interpretNF (removeDuplicates l)
    | nil => idp
    | :: a nil => idp
    | :: a (:: a1 l) => mcases \with {
      | yes p => run {
        rewrite p,
        rewriteI (removeDuplicates-consistent (a1 :: l)),
        rewrite interpretNF.cons,
        inv M.*-assoc *> pmap (`∧ interpretNF l) (<=-antisymmetric meet-left (meet-univ <=-reflexive <=-reflexive))
      }
      | no _ => run {
        rewrite {2} interpretNF.cons,
        pmap (f a ∧) (removeDuplicates-consistent (a1 :: l))
      }
    }

  \lemma normalize-consistent (t : MonoidTerm V) : interpret t = interpretNF (removeDuplicates (RedBlack.sort (normalize t)))
    => CData.normalize-consistent t *> removeDuplicates-consistent _

  \lemma terms-equality (t s : MonoidTerm V) (p : interpretNF (removeDuplicates (RedBlack.sort (normalize t))) = interpretNF (removeDuplicates (RedBlack.sort (normalize s)))) : interpret t = interpret s
    => normalize-consistent t *> p *> inv (normalize-consistent s)

  \lemma terms-equality-conv (t s : MonoidTerm V) (p : interpret t = interpret s) : interpretNF (removeDuplicates (RedBlack.sort (normalize t))) = interpretNF (removeDuplicates (RedBlack.sort (normalize s)))
    => inv (normalize-consistent t) *> p *> normalize-consistent s

} \where {
  \open Data
  \open Sort
}
