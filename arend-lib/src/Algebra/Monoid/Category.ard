\import Algebra.Monoid
\import Algebra.Pointed
\import Algebra.Pointed.Category
\import Category
\import Paths
\import Paths.Meta
\import Set.Category

\record MonoidHom \extends PointedHom {
  \override Dom : Monoid
  \override Cod : Monoid
  | func-* (x y : Dom) : func (x * y) = func x * func y
}
  \where {
    \func equals {M N : Monoid} {f g : MonoidHom M N} (p : \Pi (x : M) -> f x = g x) : f = g
      => ext p

    \open Monoid(Inv)

    \func presInv (h : MonoidHom) (e : Inv {h.Dom}) : Inv (h e.val) \cowith
      | inv => h e.inv
      | inv-left => inv (h.func-* e.inv e.val) *> pmap h e.inv-left *> h.func-ide
      | inv-right => inv (h.func-* e.val e.inv) *> pmap h e.inv-right *> h.func-ide

    \lemma presInvElem (h : MonoidHom) (e : Inv {h.Dom}) (e' : Inv (h e.val)) : h e.inv = e'.inv
      => pmap (\lam (j : Inv) => j.inv) (Inv.levelProp (\new Inv (h e.val) {
        | inv => h e.inv
        | inv-left => inv (h.func-* e.inv e.val) *> pmap h e.inv-left *> h.func-ide
        | inv-right => inv (h.func-* e.val e.inv) *> pmap h e.inv-right *> h.func-ide
      }) e')
  }

\instance MonoidCategory : Cat Monoid
  | Hom M N => MonoidHom M N
  | id => id
  | o {x y z : Monoid} (g : MonoidHom y z) (f : MonoidHom x y) => \new MonoidHom {
    | func x => g (f x)
    | func-ide => pmap g f.func-ide *> g.func-ide
    | func-* x y => pmap g (f.func-* x y) *> g.func-* (f x) (f y)
  }
  | id-left _ => idp
  | id-right _ => idp
  | o-assoc _ _ _ => idp
  | univalence (e : Iso) =>
    \let M=N => Monoid.equals (path (iso e.f e.inv (\lam x => path (\lam i => func {e.f_inv @ i} x)) (\lam y => path (\lam i => func {e.inv_f @ i} y)))) func-*
    \in (M=N, MonoidHom.equals (\lam x => Jl {Monoid} (\lam K M=K => func {transport (MonoidHom e.dom __) M=K (id e.dom)} x = coe (M=K @ __) x right) idp M=N))
  \where
    \func id (M : Monoid) : MonoidHom M M \cowith
      | func x => x
      | func-ide => idp
      | func-* _ _ => idp
