\import Algebra.Monoid
\import Algebra.Pointed
\import Algebra.Ring
\import HLevel
\import Logic
\import Order.Lattice
\import Order.PartialOrder
\import Paths
\import Relation.Equivalence
\open CMonoid(Div,Inv)
\open Preorder(PreorderC,EquivRel)

\record GCD {M : CMonoid}
            (val1 val2 gcd : M)
            (gcd|val1 : Div gcd val1)
            (gcd|val2 : Div gcd val2)
            (gcd-univ : \Pi (g : M) -> Div g val1 -> Div g val2 -> Div g gcd)

\class GCDMonoid \extends CancelCMonoid
  | isGCD (x y : E) : TruncP (GCD x y)

-- | The quotient of a monoid by the equivalence relation x ~ y iff x | y and y | x.
\func DivQuotient {M : CMonoid} => PreorderC {DivPreoder {M}}
  \where {
    -- | The preoreder on a monoid in which x <= y iff x | y.
    --   We define {@DivQuotient} as the poset completion of this preorder.
    \func DivPreoder {M : CMonoid} : Preorder M \cowith
      | <= x y => TruncP (Div x y)
      | <=-reflexive x => inP (\new Div x x ide (ide-left x))
      | <=-transitive x y z [x|y] [y|z] => \case [x|y], [y|z] \with {
        | inP x|y, inP y|z => inP (\new Div {
          | inv => Div.inv {y|z} * Div.inv {x|y}
          | inv-left =>
            (Div.inv {y|z} * Div.inv {x|y}) * x ==< *-assoc _ _ _ >==
            Div.inv {y|z} * (Div.inv {x|y} * x) ==< pmap (Div.inv {y|z} *) (Div.inv-left {x|y}) >==
            Div.inv {y|z} * y                   ==< Div.inv-left {y|z} >==
            z                                   `qed
        })
      }

    \func \infix 4 ~ {M : CMonoid} => Equivalence.~ {EquivRel {DivPreoder {M}}}

    \lemma make~ {M : CMonoid} (x y : M) (p : in~ x = {DivQuotient {M}} in~ y) : x ~ y =>
      Quotient.equalityEquiv (Preorder.EquivRel {DivPreoder {M}}) x y p

    -- | The quotient monoid is indeed a monoid.
    \instance DivQuotientMonoid {M : CMonoid} : CMonoid (DivQuotient {M})
      | ide => in~ ide
      | * => *'
      | ide-left [x] => \case [x] \as [x] \return in~ ide *' [x] = [x] \with {
        | in~ x => pmap in~ (ide-left x)
      }
      | *-assoc [x] [y] [z] => \case [x] \as [x], [y] \as [y], [z] \as [z] \return ([x] *' [y]) *' [z] = [x] *' ([y] *' [z]) \with {
        | in~ x, in~ y, in~ z => pmap in~ (*-assoc x y z)
      }
      | *-comm [x] [y] => \case [x] \as [x], [y] \as [y] \return [x] *' [y] = [y] *' [x] \with {
        | in~ x, in~ y => pmap in~ (*-comm x y)
      }
    \where {
      \func \infixl 7 *' {M : CMonoid} (x y : DivQuotient {M}) : DivQuotient {M}
        | in~ x, in~ y => in~ (x * y)
        | in~ x, ~-equiv y y' y~y' i => ~-equiv (x * y) (x * y') (\case y~y' \with {
          | (inP y|y', inP y'|y) => (inP (Div.product-left x y y' y|y'), inP (Div.product-left x y' y y'|y))
        }) i
        | ~-equiv x x' x~x' i, in~ y => ~-equiv (x * y) (x' * y) (\case x~x' \with {
          | (inP x|x', inP x'|x) => (inP (Div.product-right x x' y x|x'), inP (Div.product-right x' x y x'|x))
        }) i

      \func makeDiv {M : CancelCMonoid} (x y z : M) (~x*y=z : in~ (x * y) = {DivQuotient {M}} in~ z) : Div y z =>
        \let [x*y|z] => (make~ (x * y) z ~x*y=z).1
        \in TruncP.rec (Div.isProp y z) [x*y|z] (\lam (x*y|z : Div (x * y) z) => \new Div y z (x*y|z.inv * x) (*-assoc x*y|z.inv x y *> x*y|z.inv-left))
    }

    \open DivQuotientMonoid

    -- | If the original monoid is cancellative, then so is the quotient monoid.
    \instance DivQuotientCancelMonoid {M : CancelCMonoid} : CancelCMonoid
      | CMonoid => DivQuotientMonoid {M}
      | cancel-left [x] [y] [z] [x*y=x*z] => \case [x] \as [x], [y] \as [y], [z] \as [z], [x*y=x*z] : [x] *' [y] = [x] *' [z] \return [y] = [z] \with {
        | in~ x, in~ y, in~ z, x*y=x*z => path (~-equiv y z (
          \case make~ (x * y) (x * z) x*y=x*z \return y ~ z \with {
            | (inP x*y|x*z, inP x*z|x*y) => (inP (Div.cancel-left x y z x*y|x*z), inP (Div.cancel-left x z y x*z|x*y))
          }
        ))
      }

    -- | If the original monoid is a GCD-monoid, then so is the quotient monoid.
    \instance DivQuotientGCDMonoid {M : GCDMonoid} : GCDMonoid
      | CancelCMonoid => DivQuotientCancelMonoid {M}
      | isGCD [x] [y] => \case [x] \as [x], [y] \as [y] \return TruncP (GCD [x] [y]) \with {
        | in~ x, in~ y => TruncP.map (isGCD x y) (\lam (g : GCD x y) => \new GCD {DivQuotientCancelMonoid {M}} (in~ x) (in~ y) (in~ g.gcd) {
          | gcd|val1 => \new Div { | inv => in~ g.gcd|val1.inv | inv-left => pmap in~ g.gcd|val1.inv-left }
          | gcd|val2 => \new Div { | inv => in~ g.gcd|val2.inv | inv-left => pmap in~ g.gcd|val2.inv-left }
          | gcd-univ => \lam [g'] [g']|x [g']|y => ToProp.fromProp (
            \case [g'] \as [g'],
                  [g']|x : Div [g'] (in~ x),
                  [g']|y : Div [g'] (in~ y)
                  \return ToProp (Div [g'] (in~ g.gcd)) (Div.isProp [g'] (in~ g.gcd))
            \with {
              | in~ g', ~g'|x, ~g'|y =>
                \case Div.inv {~g'|x} \as x',
                      Div.inv-left {~g'|x} : x' * {DivQuotientCancelMonoid {M}} in~ g' = in~ x,
                      Div.inv {~g'|y} \as y',
                      Div.inv-left {~g'|y} : y' * {DivQuotientCancelMonoid {M}} in~ g' = in~ y
                \with {
                  | in~ x', ~x'*g'=x, in~ y', ~y'*g'=y =>
                    \let g'|gcd : Div g' g.gcd => g.gcd-univ g' (makeDiv x' g' x ~x'*g'=x) (makeDiv y' g' y ~y'*g'=y)
                    \in toProp {Div (in~ g') (in~ g.gcd)} (\new Div (in~ g') (in~ g.gcd) (in~ g'|gcd.inv) (pmap in~ g'|gcd.inv-left))
                }
            })
        })
      }

    \lemma inv~ide {M : CMonoid} (i : Inv {M}) : i.val ~ ide =>
      (inP (\new Div i.val ide i.inv i.inv-left), inP (\new Div ide i.val i.val (ide-right i.val)))

    \lemma uniqueUnit {M : CancelCMonoid} ([x] [y] : DivQuotient {M}) (p : [x] *' [y] = in~ ide) : [x] = in~ ide \elim [x], [y]
      | in~ x, in~ y => path (~-equiv x ide (inv~ide (\new Inv { | Div => makeDiv y x ide (pmap in~ (*-comm y x) *> p) })))

    \lemma div-eq {M : CancelCMonoid} {x y : DivQuotient {M}} (d1 : Div x y) (d2 : Div y x) : x = y =>
      \let d1=ide : d1.inv = ide => uniqueUnit d1.inv d2.inv (cancel-right (d1.inv * d2.inv) ide y (
        (d1.inv * d2.inv) * y ==< *-assoc d1.inv d2.inv y >==
        d1.inv * (d2.inv * y) ==< pmap (d1.inv *) d2.inv-left >==
        d1.inv * x            ==< d1.inv-left >==
        y                     ==< inv (ide-left y) >==
        ide * y               `qed))
      \in
        x          ==< inv (ide-left x) >==
        ide * x    ==< pmap (\lam t => t * x) (inv d1=ide) >==
        d1.inv * x ==< d1.inv-left >==
        y          `qed

    \func div-to~ {M : CancelCMonoid} {x y : M} (x|y : Div x y) : Div {DivQuotientMonoid {M}} (in~ x) (in~ y) \cowith
      | inv => in~ x|y.inv
      | inv-left => pmap in~ x|y.inv-left

    \lemma div-from~ {M : CancelCMonoid} {x y : M} (d : Div {DivQuotientMonoid {M}} (in~ x) (in~ y)) : \level (Div x y) (Div.isProp x y) =>
      \case d.inv \as i, d.inv-left : i * in~ x = in~ y \return \level (Div x y) (Div.isProp x y) \with {
        | in~ i, ~i*x=y => \case (make~ (i * x) y ~i*x=y).1 \return \level (Div x y) (Div.isProp x y) \with {
          | inP i*x|y => \new Div x y (Div.inv {i*x|y} * i) (*-assoc _ _ _ *> Div.inv-left {i*x|y})
        }
      }

    \lemma GCD-isProp {M : CancelCMonoid} (x y : DivQuotient {M}) : \level (isProp (GCD x y)) (isProp.isProp (GCD x y)) =>
      \lam (g1 g2 : GCD x y) =>
          \let p => div-eq (g2.gcd-univ g1.gcd g1.gcd|val1 g1.gcd|val2)
                           (g1.gcd-univ g2.gcd g2.gcd|val1 g2.gcd|val2)
          \in path (\lam i => \new GCD x y {
            | gcd => p @ i
            | gcd|val1 => pathOver (Div.isProp g2.gcd x (coe (\lam j => Div (p @ j) x) g1.gcd|val1 right) g2.gcd|val1) @ i
            | gcd|val2 => pathOver (Div.isProp g2.gcd y (coe (\lam j => Div (p @ j) y) g1.gcd|val2 right) g2.gcd|val2) @ i
            | gcd-univ g g|x g|y => pathOver (Div.isProp g g2.gcd (coe (\lam j => Div g (p @ j)) (g1.gcd-univ g g|x g|y) right) (g2.gcd-univ g g|x g|y)) @ i
          })

    -- | If the original monoid is cancellative, then the quotient monoid is poset.
    \instance DivQuotientPoset {M : CancelCMonoid} : Poset
      | Preorder => DivPreoder {DivQuotientMonoid {M}}
      | <=-antisymmetric x y [x|y] [y|x] => \case [x|y], [y|x] \with {
        | inP x|y, inP y|x => div-eq x|y y|x
      }

    \func gcdC {M : GCDMonoid} (x y : DivQuotient {M}) : GCD x y => TruncP.remove (GCD-isProp x y) (isGCD x y)

    \func gcd {M : GCDMonoid} (x y : DivQuotient {M}) => GCD.gcd {gcdC x y}

    \func gcd_~ {M : GCDMonoid} {x y : M} (g : GCD x y) : gcd (in~ x) (in~ y) = in~ g.gcd =>
      pmap (\lam (g : GCD (in~ x) (in~ y)) => g.gcd)
           (GCD-isProp (in~ x) (in~ y)
                       (gcdC (in~ x) (in~ y))
                       (\new GCD (in~ x) (in~ y) (in~ g.gcd) (div-to~ g.gcd|val1) (div-to~ g.gcd|val2) (\lam [g] [g]|[x] [g]|[y] =>
                         \case [g] \as [g], [g]|[x] : Div [g] (in~ x), [g]|[y] : Div [g] (in~ y) \return \level (Div [g] (in~ g.gcd)) (Div.isProp [g] (in~ g.gcd)) \with {
                           | in~ g', ~g'|x, ~g'|y => div-to~ (g.gcd-univ g' (div-from~ ~g'|x) (div-from~ ~g'|y))
                         })))

    -- | If the original monoid is a GCD-monoid, then the quotient monoid is a semilattice.
    \instance DivQuotientLattice {M : GCDMonoid} : JoinSemilattice
      | Poset => DivQuotientPoset {M}
      | join => gcd
      | join-left x y => inP (GCD.gcd|val1 {gcdC x y})
      | join-right x y => inP (GCD.gcd|val2 {gcdC x y})
      | join-univ x y g [g|x] [g|y] => \case [g|x], [g|y] \with {
        | inP g|x, inP g|y => inP (GCD.gcd-univ g g|x g|y)
      }

    \lemma gcd_*-left {M : GCDMonoid} (c a b : DivQuotient {M}) : c * gcd a b = gcd (c * a) (c * b) =>
      \let | d : GCD a b => gcdC a b
           | e : GCD (c * a) (c * b) => gcdC (c * a) (c * b)
           | cd|e : Div (c * d.gcd) e.gcd => e.gcd-univ (c * d.gcd) (Div.product-left c d.gcd a d.gcd|val1)
                                                                    (Div.product-left c d.gcd b d.gcd|val2)
           | x => cd|e.inv
           | c[xd]=e =>
             c * (x * d.gcd) ==< inv (*-assoc c x d.gcd) >==
             (c * x) * d.gcd ==< pmap (\lam t => t * d.gcd) (*-comm c x) >==
             (x * c) * d.gcd ==< *-assoc x c d.gcd >==
             x * (c * d.gcd) ==< cd|e.inv-left >==
             e.gcd           `qed
           | xd|d : Div (x * d.gcd) d.gcd =>
             d.gcd-univ (x * d.gcd)
                        (Div.cancel-left c (x * d.gcd) a (transport (\lam t => Div t (c * a)) (inv c[xd]=e) e.gcd|val1))
                        (Div.cancel-left c (x * d.gcd) b (transport (\lam t => Div t (c * b)) (inv c[xd]=e) e.gcd|val2))
      \in pmap (c *) (<=-antisymmetric d.gcd (x * d.gcd) (inP (\new Div d.gcd (x * d.gcd) x idp)) (inP xd|d)) *> c[xd]=e

    \lemma gcd_*-right {M : GCDMonoid} (a b c : DivQuotient {M}) : gcd a b * c = gcd (a * c) (b * c) =>
      *-comm (gcd a b) c *> gcd_*-left c a b *> pmap2 gcd (*-comm c a) (*-comm c b)

    \lemma gcd_*-comm {M : GCDMonoid} (a b c : DivQuotient {M}) : gcd a (b * c) = gcd a (gcd a b * c) =>
      gcd a (b * c)               ==< pmap (\lam t => gcd t (b * c)) (inv (JoinSemilattice.join_<= a (a * c) (inP (\new Div a (a * c) c (*-comm c a))))) >==
      gcd (gcd a (a * c)) (b * c) ==< JoinSemilattice.join-assoc a (a * c) (b * c) >==
      gcd a (gcd (a * c) (b * c)) ==< pmap (gcd a) (inv (gcd_*-right a b c)) >==
      gcd a (gcd a b * c)         `qed

    \lemma gcd_*_div~ {M : GCDMonoid} (a b c : DivQuotient {M}) (a|bc : Div a (b * c)) : \level (Div a (gcd a b * c)) (Div.isProp a (gcd a b * c)) =>
      transport (\lam t => Div t (gcd a b * c))
                (inv (gcd_*-comm a b c) *> JoinSemilattice.join_<= a (b * c) (inP a|bc))
                (GCD.gcd|val2 {gcdC a (gcd a b * c)})

    \lemma gcd_*_div {M : GCDMonoid} (a b c : M) (a|bc : Div a (b * c)) (a_b : GCD a b ide) : \level (Div a c) (Div.isProp a c) =>
      div-from~ (transport (\lam t => Div (in~ a) t)
                           (pmap (\lam t => t * in~ c) (gcd_~ a_b) *> ide-left (in~ c))
                           (gcd_*_div~ (in~ a) (in~ b) (in~ c) (div-to~ a|bc)))
  }
