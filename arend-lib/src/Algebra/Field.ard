\import Algebra.Domain
\import Algebra.Group
\import Algebra.Monoid
\import Algebra.Ring
\import Algebra.Ring.Local
\import Logic
\import Paths
\import Paths.Meta
\import Set
\open Monoid(Inv)

-- | A field is a commutative local ring such that non-invertible elements equal to {zro}.
\class Field \extends LocalCRing, IntegralDomain
  | #0 x => Inv x
  | #0-+ => LocalRing.sumInv=>eitherInv
  | #0-zro (j : Inv zro) => zro/=ide (inv zro_*-right *> j.inv-left)
  | #0-*-left {x} {y} => Inv.cfactor-left x y
  | zro#ide => Inv.lmake ide ide-left
  | apartZeroProduct x y xj yj => Inv.product xj yj
  \where {
    \open AddGroup(negative-isInv)
    \open Ring
  }

-- | A discrete field is a commutative ring such that every element is either {zro} or invertible.
\class DiscreteField \extends Field, IntegralDomain.Dec {
  | eitherZeroOrInv (x : E) : (x = zro) || (Inv x)

  | locality x => \case eitherZeroOrInv x \with {
    | byLeft x=0 => byRight (Inv.lmake ide (
        ide * (x + ide) ==< ide-left >==
        x + ide         ==< pmap (`+ ide) x=0 >==
        zro + ide       ==< zro-left >==
        ide             `qed))
    | byRight xInv => byLeft xInv
  }
  \default decideEq x y => \case eitherZeroOrInv (x - y) \with {
    | byLeft x-y=0 => yes (AddGroup.fromZero x y x-y=0)
    | byRight x-y-isInv => no (\lam x=y => #-irreflexive (rewriteF x=y x-y-isInv))
  }
  | nonEqualApart x x/=0 => \case eitherZeroOrInv x \with {
    | byLeft x=0 => absurd (x/=0 x=0)
    | byRight x#0 => x#0
  }
}