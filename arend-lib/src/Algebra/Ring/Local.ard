\import Algebra.Group
\import Algebra.Monoid
\import Algebra.Ring
\import Logic
\import Paths
\import Paths.Meta
\open Monoid(Inv,LInv)

\class LocalRing \extends Ring {
  | zro/=ide : Not (zro = ide)
  | locality (x : E) : Inv x || Inv (x + ide)

  \lemma sum1=>eitherInv (x y : E) (x+y=1 : x + y = ide) : Inv x || Inv y => \case locality (negative x) \with {
    | byLeft -x_inv => byLeft (rewriteI (AddGroup.negative-isInv x) (Ring.negative_inv -x_inv))
    | byRight [-x+1]_inv => byRight (rewriteI (
        negative x + ide     ==< pmap (negative x +) (inv x+y=1) >==
        negative x + (x + y) ==< inv +-assoc >==
        (negative x + x) + y ==< pmap (`+ y) (negative-left x) >==
        zro + y              ==< zro-left >==
        y                    `qed) [-x+1]_inv)
  }

  \lemma sumInv=>eitherInv (x y : E) (q : Inv (x + y)) : Inv x || Inv y =>
    \case sum1=>eitherInv (q.inv * x) (q.inv * y) (inv ldistr *> q.inv-left) \with {
      | byLeft s => byLeft (Inv.factor-right q.inv x (\new LInv q.inv (x + y) q.inv-right) s)
      | byRight s => byRight (Inv.factor-right q.inv y (\new LInv q.inv (x + y) q.inv-right) s)
    }
}

\class LocalCRing \extends LocalRing, CRing
