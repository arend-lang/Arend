\import Function.Meta
\import Logic
\import Paths
\open Nat

-- # Various operations

\func \infixl 6 -' (n m : Nat) : Nat
  | 0, _ => 0
  | suc n, 0 => suc n
  | suc n, suc m => n -' m

\func pred (x : Nat) : Nat
  | zero => 0
  | suc x' => x'

\lemma suc_pred {n : Nat} (n/=0 : n /= 0) : suc (pred n) = n \elim n
  | 0 => absurd (n/=0 idp)
  | suc n => idp

\lemma -'0 {n : Nat} : n -' 0 = n \elim n
  | 0 => idp
  | suc n => idp

\lemma -'+ {n m : Nat} : n + m -' m = n \elim m
  | 0 => -'0
  | suc n => -'+

\lemma -'id {n : Nat} : n -' n = 0 => -'+ {0}

\lemma -'-' {a b c : Nat} : a -' b -' c = a -' (b + c) \elim a, b
  | 0, b => idp
  | suc a, 0 => idp
  | suc a, suc b => -'-'

\func suc/=0 {n : Nat} (p : suc n = 0) : Empty

-- # The order relations

\module NatOrder \where {
  \data \infix 4 < (n m : Nat) \with
    | 0, suc _ => zero<suc
    | suc n, suc m => suc<suc (n < m)

  \lemma unsuc< {n m : Nat} (p : suc n < suc m) : n < m \elim p
    | suc<suc p => p
}

\open NatOrder

\lemma -'_< {n m : Nat} (p : 0 < n -' m) : m < n \elim n, m, p
  | suc n, 0, _ => zero<suc
  | suc n, suc m, p => suc<suc (-'_< p)

\lemma <_-' {n m : Nat} (p : m < n) : 0 < n -' m \elim n, m, p
  | suc n, 0, _ => zero<suc
  | suc n, suc m, suc<suc p => <_-' p

\lemma id<suc {n : Nat} : n < suc n \elim n
  | 0 => zero<suc
  | suc n => suc<suc id<suc

\lemma id/=suc {n : Nat} : n /= suc n \elim n
  | 0 => \case __
  | suc n => \lam p => id/=suc {n} (pmap pred p)

\lemma nonZero>0 {n : Nat} (n/=0 : n /= 0) : 0 < n \elim n
  | 0 => absurd (n/=0 idp)
  | suc n => zero<suc

-- # Fin properties

\lemma fin_< {n : Nat} (x : Fin n) : x < n \elim n, x
  | suc n, zero => zero<suc
  | suc n, suc x => suc<suc (fin_< x)

\func toFin (k : Nat) {n : Nat} (\property p : k < n) : Fin n \elim k, n
  | 0, suc n => 0
  | suc k, suc n => suc (toFin _ (unsuc< p))

\lemma toFin=id {k n : Nat} {p : k < n} : toFin k p = {Nat} k \elim k, n, p
  | 0, suc n, zero<suc => idp
  | suc k, suc n, suc<suc p => pmap suc toFin=id

\lemma toFin=fin {n : Nat} {k : Fin n} : toFin k (fin_< k) = k
  => fin_nat-inj toFin=id

\func toFin' {k n : Nat} (p : k < n) : Fin n \elim n
  | suc n => k mod suc n

\func mod_Fin (k : Nat) {n : Nat} (p : 0 < n) : Fin n \elim n
  | suc n => k mod suc n

\lemma mod_Fin=mod {k n : Nat} {p : 0 < n} : mod_Fin k p = {Nat} k mod n \elim n
  | suc n => idp

\lemma fin_nat-inj {n : Nat} {x y : Fin n} (p : x = {Nat} y) : x = y \elim n, x, y, p
  | suc n, zero, zero, _ => idp
  | suc n, suc x, suc y, p => pmap (\lam z => suc z) (fin_nat-inj (pmap pred p))

\lemma fin_nat-ineq {n : Nat} {x y : Fin n} (p : Not (x = y)) : Not (x = {Nat} y)
  => \lam q => p (fin_nat-inj q)
