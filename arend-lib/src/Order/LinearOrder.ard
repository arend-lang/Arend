\import Category
\import Data.Or
\import Logic
\import Order.Lattice
\import Order.PartialOrder
\import Order.StrictOrder
\import Paths
\import Paths.Meta
\import Set
\open MeetSemilattice (meet_<=, meet-comm)
\open JoinSemilattice (join_<=, join-comm, join-idemp)

\class TotalOrder \extends DistributiveLattice {
  | totality (x y : E) : x <= y || y <= x

  | join x y => (tjoin (totality x y)).1
  | join-left {x} {y} => (tjoin (totality x y)).2.1
  | join-right {x} {y} => (tjoin (totality x y)).2.2
  | join-univ {x} {y} {z} => (tjoin (totality x y)).2.3 z

  | meet x y => (tmeet (totality x y)).1
  | meet-left {x} {y} => (tmeet (totality x y)).2.1
  | meet-right {x} {y} => (tmeet (totality x y)).2.2
  | meet-univ {x} {y} {z} => (tmeet (totality x y)).2.3 z

  | distrib>= {_} {_} {_} => rewriteI (total-distrib totality) <=-reflexive
} \where {
  \func tjoin {E : Poset} {x y : E} => ||.rec JoinSemilattice.Join-isProp
                                              (\lam x<=y => (x, (E.<=-reflexive, x<=y, \lam z z<=x _ => z<=x)))
                                              (\lam y<=x => (y, (y<=x, E.<=-reflexive, \lam z _ z<=y => z<=y)))

  \func tmeet {E : Poset} {x y : E} => ||.rec MeetSemilattice.Meet-isProp
                                              (\lam x<=y => (y, (x<=y, E.<=-reflexive, \lam z _ y<=z => y<=z)))
                                              (\lam y<=x => (x, (E.<=-reflexive, y<=x, \lam z z<=x _ => z<=x)))

  \lemma total-distrib {E : Lattice} (totality : \Pi (x y : E) -> x <= y || y <= x) {x y z : E} : meet x (join y z) = join (meet x y) (meet x z) =>
    \case totality y z \with {
      | byLeft y<=z => distrib-x-y totality y<=z
      | byRight z<=y => transport2 (meet x __ = __) join-comm join-comm (distrib-x-y totality z<=y)
    } \where {
    \lemma distrib-x-y {E : Lattice} (totality : \Pi (x y : E) -> x <= y || y <= x)
                       {x y z : E} (y<=z : y <= z) : meet x (join y z) = join (meet x y) (meet x z) =>
      pmap (meet x) (join_<= y<=z) *>
      (\case totality x y \with {
        | byLeft x<=y =>
          meet_<= x<=y *>
          inv (join_<= y<=z) *>
          pmap2 join (inv (meet_<= x<=y)) (inv (meet_<= (x<=y Precat.>> y<=z)))
        | byRight y<=x =>
          meet-comm *>
          meet_<= y<=x *>
          (\case totality x z \with {
            | byLeft x<=z =>
              inv (join_<= x<=z) *>
              pmap2 join (inv (meet-comm *> meet_<= y<=x)) (inv (meet_<= x<=z))
            | byRight z<=x =>
              inv join-idemp *>
              pmap2 join (inv (meet-comm *> meet_<= y<=x)) (inv (meet-comm *> meet_<= z<=x))
          })
      })
  }
}

-- | A linearly ordered set.
\class LinearOrder \extends StrictPoset {
  | <-comparison (x y z : E) : x < z -> x < y || y < z
  | <-connectedness {x y : E} : Not (x < y) -> Not (y < x) -> x = y
} \where {
  \class With# \extends LinearOrder, Set#
    | # x y => x < y || y < x
    | #-irreflexive x#x => \case x#x \with {
      | byLeft x<x => <-irreflexive x<x
      | byRight x<x => <-irreflexive x<x
    }
    | #-symmetric x#y => \case x#y \with {
      | byLeft x<y => byRight x<y
      | byRight y<x => byLeft y<x
    }
    | #-comparison x y z x#z => \case x#z \with {
      | byLeft x<z => \case <-comparison x y z x<z \with {
        | byLeft x<y => byLeft (byLeft x<y)
        | byRight y<z => byRight (byLeft y<z)
      }
      | byRight z<x => \case <-comparison z y x z<x \with {
        | byLeft z<y => byRight (byRight z<y)
        | byRight y<x => byLeft (byRight y<x)
      }
    }
    | tightness x/#y => <-connectedness (\lam x<y => x/#y (byLeft x<y)) (\lam y<x => x/#y (byRight y<x))

  -- | A linearly ordered set with decidable inequality.
  \class Dec \extends LinearOrder, DecSet {
    \property trichotomy (x y : E) : (x = y) `Or` (x < y) `Or` (y < x)

    | <-comparison x y z x<z => \case trichotomy x y \with {
      | inl x=y => byRight (transport (`< z) x=y x<z)
      | inr (inl x<y) => byLeft x<y
      | inr (inr y<x) => byRight (<-transitive y<x x<z)
    }
    | <-connectedness {x} {y} x/<y y/<x => \case trichotomy x y \with {
      | inl x=y => x=y
      | inr (inl x<y) => absurd (x/<y x<y)
      | inr (inr y<x) => absurd (y/<x y<x)
    }

    | decideEq x y => \case trichotomy x y \with {
      | inl x=y => yes x=y
      | inr (inl x<y) => no (\lam x=y => <-irreflexive (transport (`< y) x=y x<y))
      | inr (inr y<x) => no (\lam x=y => <-irreflexive (transport (y <) x=y y<x))
    }
  } \where {
    \func =>TotalOrder {L : Dec} : TotalOrder L \cowith
      | <= x y => (x = y) || x < y
      | <=-reflexive {x} => byLeft idp
      | <=-transitive {x} {_} {z} x<=y y<=z => \case x<=y, y<=z \with {
        | byLeft x=y, byLeft y=z => byLeft (x=y *> y=z)
        | byLeft x=y, byRight y<z => byRight (transport (`< z) (inv x=y) y<z)
        | byRight x<y, byLeft y=z => byRight (transport (x <) y=z x<y)
        | byRight x<y, byRight y<z => byRight (<-transitive x<y y<z)
      }
      | totality x y => \case trichotomy x y \with {
        | inl x=y => byLeft (byLeft x=y)
        | inr (inl x<y) => byLeft (byRight x<y)
        | inr (inr y<x) => byRight (byRight y<x)
      }
      | <=-antisymmetric x<=y y<=x => \case x<=y, y<=x \with {
        | byLeft x=y, _ => x=y
        | _, byLeft y=x => inv y=x
        | byRight x<y, byRight y<x => absurd (<-irreflexive (<-transitive x<y y<x))
      }

    \class Impl \extends Dec, DecSet.Impl
  }
}
