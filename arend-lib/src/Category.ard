\import Equiv(Embedding, Equiv, Retraction)
\import Equiv.Fiber
\import Equiv.Path
\import Equiv.Sigma
\import Equiv.Univalence
\import HLevel
\import Paths \using (inv \as pinv)
\import Paths.Meta
\import Set.Category

\class Precat (Ob : \hType) {
  | Hom : Ob -> Ob -> \Set
  | id (X : Ob) : Hom X X
  | \fixr 8 o \alias \infixr 8 ∘ {X Y Z : Ob} : Hom Y Z -> Hom X Y -> Hom X Z
  | id-left {X Y : Ob} (f : Hom X Y) : id Y ∘ f = f
  | id-right {X Y : Ob} (f : Hom X Y) : f ∘ id X = f
  | o-assoc {X Y Z W : Ob} (f : Hom X Y) (g : Hom Y Z) (h : Hom Z W) : h ∘ (g ∘ f) = (h ∘ g) ∘ f

  \func \infixl 8 >> {x y z : Ob} (f : Hom x y) (g : Hom y z) => g ∘ f
}

\open Precat (>>)

\func opP (C : Precat) : Precat \cowith
  | Ob => C.Ob
  | Hom x y => C.Hom y x
  | id x => C.id x
  | o g f => C.o f g
  | id-left f => C.id-right f
  | id-right f => C.id-left f
  | o-assoc f g h => pinv (C.o-assoc h g f)

\record Map {C : Precat} {dom cod : C} (\coerce f : Hom dom cod)

\record Mono \extends Map
  | isMono {x : C} (g h : Hom x dom) : g >> f = h >> f -> g = h

\func isEpi {C : Precat} {x y : C} (f : Hom x y) => \Pi {z : C} (g h : Hom y z) -> f >> g = f >> h -> g = h

\record SplitMono (inv : Hom cod dom) (f_inv : f >> inv = id dom) \extends Mono
  | isMono g h gf=hf =>
    g               ==< pinv (id-left g) >==
    g >> id dom     ==< pmap (g >>) (pinv f_inv) >==
    g >> (f >> inv) ==< pinv (o-assoc g f inv) >==
    (g >> f) >> inv ==< pmap (o inv) gf=hf >==
    (h >> f) >> inv ==< o-assoc h f inv >==
    h >> (f >> inv) ==< pmap (h >>) f_inv >==
    h >> id dom     ==< id-left h >==
    h               `qed

\record Iso (inv_f : inv >> f = id cod) \extends SplitMono
  \where {
    \func equals {C : Precat} {x y : C} (e e' : Iso {C} {x} {y}) (p : e.f = e'.f) : e = e'
      => ext (p, isMono {e} e.inv e'.inv (e.inv_f *> pinv e'.inv_f *> pinv (pmap (e'.inv >>) p)))

    \use \level levelProp {C : Precat} {x y : C} (f : Hom x y) (e e' : Iso f) => equals e e' idp
  }

\func idIso {C : Precat} {x : C} : Iso (id x) \cowith
  | inv => id x
  | inv_f => id-left (id x)
  | f_inv => id-right (id x)

\class Cat \extends Precat {
  | univalence : \Pi (f : Iso) -> \Sigma (p : f.dom = f.cod) (transport (Hom f.dom) p (id f.dom) = f)
    \level uni-isProp {\this}

  \func iso-to-= (e : Iso {\this}) : e.dom = e.cod
    => (univalence e).1

  \lemma transport-Hom (e : Iso {(\this : Cat)}) {z : Ob} (g : Hom z e.dom) : transport (Hom z) (iso-to-= e) g = g >> e.f
    => Jl (\lam _ q => transport (Hom z) q g = g >> transport (Hom e.dom) q (id e.dom)) (pinv (id-left g)) (iso-to-= e)
       *> pmap (g >>) (univalence e).2
} \where
    \func uni-isProp {C : Precat} (F G : \Pi (f : Iso {C}) -> \Sigma (p : f.dom = f.cod) (transport (Hom f.dom) p (id f.dom) = f)) : F = G
      => \let | T {x y : C} (p : x = y) => transport (Iso {C} {x} {__}) p idIso
              | Emb {x : C} => \new Embedding (\lam (e : Iso {C} {x} {x}) => e.f) (\lam (e e' : Iso {C} {x} {x}) => \new Retraction {
                | sec => Iso.equals e e'
                | f_sec => idpe
              })
              | Eq {x y : C} (e : Iso {C} {x} {y}) (p : x = y)
                  => Jl (\lam y' p' => \Pi (e' : Iso {C} {x} {y'}) -> Equiv {transport (Iso {C} {x} {__}) p' idIso = {Iso {C} {x} {y'}} e'} {transport (Hom x) p' (id x) = e'.f})
                        (\lam e' => Embedding.pmap-isEquiv {Emb {x}} {idIso} {e'})
                        p e
              | T-equiv {x y : C} : Equiv (T {x} {y}) => pathEquiv (Iso {C} {__} {__}) (\lam {x} {y} => \new Retraction {
                | f => T {x} {y}
                | sec (e : Iso) => (F e).1
                | f_sec (e : Iso) => Equiv.ret {Eq e (F e).1} (F e).2
              }) {x} {y}
              | Fib-equiv {x y : C} (e : Iso {C} {x} {y}) => transport isProp (Equiv-to-= (sigma-right (\lam p => transport (Iso {C} {x} {__}) p idIso = {Iso {C} {x} {y}} e) (\lam p => transport (Hom x) p (id x) = e.f) (Eq e))) (isContr=>isProp (Equiv=>contrFibers (T-equiv {x} {y}) e))
         \in path (\lam i f => Fib-equiv f (F f) (G f) @ i)

\func op (C : Cat) : Cat => \new Cat {
  | Precat => opP C
  | univalence (f : Iso) =>
    \case f.cod \as cod, f.f \as ff : Hom cod f.dom, f.inv \as inv : Hom f.dom cod, f.f_inv \as f_inv : inv >> ff = id f.dom, f.inv_f \as inv_f : ff >> inv = id cod,
          C.univalence (\new Iso inv ff f_inv inv_f) \return \Sigma (p : f.dom = cod) (transport (Hom {opP C} f.dom) p (id f.dom) = ff) \with {
      | cod, ff, inv, f_inv, inv_f, (idp,idp) => (idp, pinv f_inv *> id-right ff)
    }
}

\sfunc SIP (C : Cat) (Str : C -> \hType (\suc \lp)) (isHom : \Pi {x y : C} -> Str x -> Str y -> Hom x y -> \hType (\suc \lp))
           (st : \Pi {X : C} {S1 S2 : Str X} -> isHom S1 S2 (id X) -> isHom S2 S1 (id X) -> S1 = S2)
           {X Y : C} (e : Iso {C} {X} {Y}) (S1 : Str X) (S2 : Str Y) (p : isHom S1 S2 e) (q : isHom S2 S1 e.inv)
  : \Sigma (p : X = Y) (Path (\lam i => Str (p @ i)) S1 S2) (transport (Hom X) p (id X) = e)
  => \case \elim Y, \elim e, \elim S2, p, q, univalence e \with {
    | Y, e : Iso, S2, p, q, (idp,s) => (idp, st (transportInv (isHom S1 S2) s p) (transport (isHom S2 S1) (pinv (id-right e.inv) *> pmap (∘ e.inv) s *> e.f_inv) q), s)
  }

\sfunc SIP_Set (Str : \Set -> \hType (\suc \lp)) (isHom : \Pi {x y : \Set} -> Str x -> Str y -> (x -> y) -> \hType (\suc \lp))
               (st : \Pi {X : \Set} {S1 S2 : Str X} -> isHom S1 S2 (\lam x => x) -> isHom S2 S1 (\lam x => x) -> S1 = S2)
               {X Y : \Set} (e : Iso {SetCat} {X} {Y}) (S1 : Str X) (S2 : Str Y) (p : isHom S1 S2 e.f) (q : isHom S2 S1 e.inv)
  : \Sigma (p : X = Y) (Path (\lam i => Str (p @ i)) S1 S2) (\Pi (x : X) -> transport (\lam Z => Z) p x = e.f x)
  => \have (p,q,s) => SIP SetCat Str isHom st e S1 S2 p q
     \in (p, q, \lam x => pinv (transport_pi (\lam _ => X) (\lam Z => Z) p (\lam z => z) x) *> path (\lam i => (s @ i) x))
