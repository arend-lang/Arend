\import Equiv(Embedding, Equiv, Retraction)
\import Equiv.Fiber
\import Equiv.Path
\import Equiv.Sigma
\import Equiv.Univalence
\import HLevel
\import Paths \using (inv \as pinv)
\import Paths.Meta

\class Precat (Ob : \hType) {
  | Hom : Ob -> Ob -> \Set
  | id (x : Ob) : Hom x x
  | \fixr 8 o \alias \infixr 8 ∘ {x y z : Ob} : Hom y z -> Hom x y -> Hom x z
  | id-left {x y : Ob} (f : Hom x y) : id y ∘ f = f
  | id-right {x y : Ob} (f : Hom x y) : f ∘ id x = f
  | o-assoc {x y z w : Ob} (f : Hom x y) (g : Hom y z) (h : Hom z w) : h ∘ (g ∘ f) = (h ∘ g) ∘ f

  \func \infixl 8 >> {x y z : Ob} (f : Hom x y) (g : Hom y z) => g ∘ f
}

\open Precat (>>)

\func opP (C : Precat) : Precat \cowith
  | Ob => C.Ob
  | Hom x y => C.Hom y x
  | id x => C.id x
  | o g f => C.o f g
  | id-left f => C.id-right f
  | id-right f => C.id-left f
  | o-assoc f g h => pinv (C.o-assoc h g f)

\record Map {C : Precat} {dom cod : C} (f : Hom dom cod)

\record Mono \extends Map
  | isMono {x : C} (g h : Hom x dom) : g >> f = h >> f -> g = h

\func isEpi {C : Precat} {x y : C} (f : Hom x y) => \Pi {z : C} (g h : Hom y z) -> f >> g = f >> h -> g = h

\record SplitMono (inv : Hom cod dom) (f_inv : f >> inv = id dom) \extends Mono
  | isMono g h gf=hf =>
    g               ==< pinv (id-left g) >==
    g >> id dom     ==< pmap (g >>) (pinv f_inv) >==
    g >> (f >> inv) ==< pinv (o-assoc g f inv) >==
    (g >> f) >> inv ==< pmap (o inv) gf=hf >==
    (h >> f) >> inv ==< o-assoc h f inv >==
    h >> (f >> inv) ==< pmap (h >>) f_inv >==
    h >> id dom     ==< id-left h >==
    h               `qed

\record Iso (inv_f : inv >> f = id cod) \extends SplitMono
  \where {
    \func equals {C : Precat} {x y : C} (e e' : Iso {C} {x} {y}) (p : e.f = e'.f) : e = e'
      => ext (p, isMono {e} e.inv e'.inv (e.inv_f *> pinv e'.inv_f *> pinv (pmap (e'.inv >>) p)))

    \use \level levelProp {C : Precat} {x y : C} (f : Hom x y) (e e' : Iso f) => equals e e' idp
  }

\func idIso {C : Precat} {x : C} : Iso (id x) \cowith
  | inv => id x
  | inv_f => id-left (id x)
  | f_inv => id-right (id x)

\class Cat \extends Precat {
  | univalence : \Pi {x y : Ob} (f : Hom x y) (g : Hom y x) (p : g ∘ f = id x) (q : f ∘ g = id y) -> \Sigma (p : x = y) (transport (Hom x) p (id x) = f)
    \level uni-isProp {\this}

  \func iso-to-= (e : Iso {(\this : Cat)}) : e.dom = e.cod
    => (univalence e.f e.inv e.f_inv e.inv_f).1

  \lemma transport-Hom (e : Iso {(\this : Cat)}) {z : Ob} (g : Hom z e.dom) : transport (Hom z) (iso-to-= e) g = g >> e.f
    => Jl (\lam _ q => transport (Hom z) q g = g >> transport (Hom e.dom) q (id e.dom)) (pinv (id-left g)) (iso-to-= e)
       *> pmap (g >>) (univalence e.f e.inv e.f_inv e.inv_f).2
} \where
    \func uni-isProp {C : Precat} (F G : \Pi {x y : C} (f : Hom x y) (g : Hom y x) (p : g ∘ f = id x) (q : f ∘ g = id y) -> \Sigma (p : x = y) (transport (Hom x) p (id x) = f))
      : (\lam {x} {y} => F {x} {y}) = (\lam {x} {y} => G {x} {y})
      => \let | T {x y : C} (p : x = y) => transport (Iso {C} {x} {__}) p idIso
              | Emb {x : C} => \new Embedding (\lam (e : Iso {C} {x} {x}) => e.f) (\lam (e e' : Iso {C} {x} {x}) => \new Retraction {
                | sec => Iso.equals e e'
                | f_sec => idpe
              })
              | Eq {x y : C} (e : Iso {C} {x} {y}) (p : x = y)
                  => Jl (\lam y' p' => \Pi (e' : Iso {C} {x} {y'}) -> Equiv {transport (Iso {C} {x} {__}) p' idIso = {Iso {C} {x} {y'}} e'} {transport (Hom x) p' (id x) = e'.f})
                        (\lam e' => Embedding.pmap-isEquiv {Emb {x}} {idIso} {e'})
                        p e
              | t (e : Iso) => F e.f e.inv e.f_inv e.inv_f
              | T-equiv {x y : C} : Equiv (T {x} {y}) => pathEquiv (Iso {C} {__} {__}) (\lam {x} {y} => \new Retraction {
                | f => T {x} {y}
                | sec (e : Iso) => (t e).1
                | f_sec (e : Iso) => Equiv.ret {Eq e (t e).1} (t e).2
              }) {x} {y}
              | Fib-equiv {x y : C} (e : Iso {C} {x} {y}) => transport isProp (Equiv-to-= (sigma-right (\lam p => transport (Iso {C} {x} {__}) p idIso = {Iso {C} {x} {y}} e) (\lam p => transport (Hom x) p (id x) = e.f) (Eq e))) (isContr=>isProp (Equiv=>contrFibers (T-equiv {x} {y}) e))
         \in path (\lam i {x} {y} f g p q => Fib-equiv (\new Iso { | f => f | inv => g | f_inv => p | inv_f => q }) (F f g p q) (G f g p q) @ i)

\func op (C : Cat) : Cat => \new Cat {
  | Precat => opP C
  | univalence {x} {y} f g p q =>
    \case C.univalence g f p q \with {
      | (s,t) => \case \elim y, \elim f, \elim s, \elim g, \elim t, \elim p \with {
        | y, f, idp, g, idp, p => (idp, pinv p *> id-right f)
      }
    }
}
