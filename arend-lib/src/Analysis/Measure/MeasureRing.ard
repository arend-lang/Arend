\import Algebra.Ordered
\import Algebra.Ring.Boolean
\import Arith.Real.InfReal
\import Function.Meta
\import Paths
\import Topology.MetricSpace.ExtendedMetricSpace
\import Topology.NormedAbGroup.ValuedNormedAbGroup

\class PseudoMeasureRing \extends BooleanPseudoRing, LinearlyValuedPseudoNormedAbGroup, ExtendedPseudoMetricSpace
  | norm-positive {x : E} : 0 InfRealAbMonoid.<= norm x
  | norm-measure {x y : E} : norm x InfRealAbMonoid.+ norm y = norm (x ∨ y) InfRealAbMonoid.+ norm (x ∧ y)
  | norm-mono {x y : E} : x <= y -> norm x InfRealAbMonoid.<= norm y
  | norm-mono p => transport2 (InfRealAbMonoid.<=) InfRealAbMonoid.zro-left (norm-measure *> pmap2 (norm __ InfRealAbMonoid.+ __) (pmap2 (+) (+-assoc *> pmap (_ +) (pmap (`+ _) (*-comm *> p) *> double=0) *> zro-right) diff_* *> zro-right) (pmap norm diff_* *> norm_zro) *> InfRealAbMonoid.zro-right) $ <=_+ norm-positive InfRealAbMonoid.<=-refl
  | norm_negative => pmap norm neative=id
  | norm_+ => transport (InfRealAbMonoid.`<= _) (norm-measure *> pmap2 (norm __ InfRealAbMonoid.+ __) (inv +_diff) (pmap norm diff_*_diff *> norm_zro) *> InfRealAbMonoid.zro-right) $ <=_+ (norm-mono diff_<=) (norm-mono diff_<=)

\class PseudoMeasureAlgebra \extends PseudoMeasureRing, BooleanRing