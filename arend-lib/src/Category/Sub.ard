\import Category
\import Category.Adjoint
\import Category.Functor
\import Category.Limit
\import Equiv \hiding (Map)
\import Function.Meta
\import Meta
\import Paths
\import Paths.Meta

\func subPrecat {C : Precat} {X : \Type} (f : X -> C) : Precat X \cowith
  | Hom x y => Hom (f x) (f y)
  | id x => id (f x)
  | o h g => o h g
  | id-left => id-left
  | id-right => id-right
  | o-assoc => o-assoc
  \where
    \func embedding {C : Precat} {X : \Type} (f : X -> C) : FullyFaithfulFunctor (subPrecat f) C f \cowith
      | Func h => h
      | Func-id => idp
      | Func-o => idp
      | isFullyFaithful => idEquiv

\func subCat {C : Cat} {X : \Type} (e : Embedding {X} {C}) : Cat \cowith
  | Precat => subPrecat e
  | univalence (j : Iso) =>
    \have (t,r) => univalence (\new Iso j.f j.inv j.inv_f j.f_inv)
    \in (Retraction.sec {e.isEmb j.dom j.cod} t, pmap (transport (Hom (e j.dom)) __ (id (e j.dom))) (Retraction.f_sec {e.isEmb j.dom j.cod} t) *> r)

\class ReflectiveSubPrecat \extends FullyFaithfulFunctor, RightAdjoint
  | reflector : D -> C
  | reflectorMap (X : D) : Hom X (F (reflector X))
  | isReflective {X : D} {Y : C} : Equiv {Hom (reflector X) Y} {Hom X (F Y)} (Func __ ∘ reflectorMap X)
  | leftAdjoint {
      | F => reflector
      | Func {X} {Y} h => isReflective.ret (reflectorMap Y ∘ h)
      | Func-id => rewrite id-right (unfold (rewrite Func-id $ rewrite id-left idp) *> isReflective.ret_f (id _))
      | Func-o => run {
          isReflective.isInj,
          unfold,
          rewrite isReflective.f_ret,
          rewrite Func-o,
          rewrite o-assoc,
          rewrite isReflective.f_ret,
          rewriteI {2} o-assoc,
          rewrite isReflective.f_ret,
          inv o-assoc
        }
    }
  | eta {
    | trans => reflectorMap
    | natural h => inv (isReflective.f_ret _)
  }
  | isAdjoint => isReflective
  \where {
    \func fromRightAdjoint (F : RightAdjoint) {ff : FullyFaithfulFunctor { | Functor => F }} : ReflectiveSubPrecat \cowith
      | FullyFaithfulFunctor => ff
      | reflector => F.leftAdjoint
      | reflectorMap => F.eta
      | isReflective => isAdjoint
  }

\func reflectiveSubPrecatColimit {J : SmallPrecat} (I : ReflectiveSubPrecat) (F : Functor J (opP I.C)) (c : Limit (Comp I.op F)) : Limit F \cowith
  | apex => I.reflector c.apex
  | coneMap j => I.inverse (reflectorMap _ ∘ c.coneMap j)
  | coneCoh h => I.isFaithful $ run {
    rewrite I.Func-o,
    repeat {2} (rewrite I.inverse-right),
    rewrite o-assoc,
    pmap (_ ∘) (c.coneCoh h)
  }
  | isLimit z => TwoOutOfThree.rightFactorPath
      (Cone.mapEquiv I.op {J} {F} z)
      (transEquiv I.isReflective (c.isLimit (I z)))
      (ext (\lam h => exts (\lam j => I.Func-o *> pmap (_ ∘) (I.inverse-right _) *> inv o-assoc)))
