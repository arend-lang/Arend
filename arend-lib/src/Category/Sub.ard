\import Category
\import Equiv (Embedding, Retraction)
\import Paths

\func subPrecat {C : Precat} {X : \Type} (f : X -> C) : Precat X \cowith
  | Hom x y => Hom (f x) (f y)
  | id x => id (f x)
  | o h g => o h g
  | id-left g => id-left g
  | id-right g => id-right g
  | o-assoc g h k => o-assoc g h k

\func subCat {C : Cat} {X : \Type} (e : Embedding {X} {C}) : Cat \cowith
  | Precat => subPrecat e.f
  | univalence (j : Iso) =>
    \let (t,r) => univalence (\new Iso j.f j.inv j.f_inv j.inv_f)
    \in (Retraction.sec {e.isEmb j.dom j.cod} t, pmap (transport (Hom (e.f j.dom)) __ (id (e.f j.dom))) (Retraction.f_sec {e.isEmb j.dom j.cod} t) *> r)
