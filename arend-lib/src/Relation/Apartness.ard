\import Algebra.Pointed
\import Logic
\import Paths
\import Set

\class Apartness \extends BaseSet
  | \infix 4 # : E -> E -> \Prop
  | #-irreflexive (x : E) : Not (x # x)
  | #-symmetric (x y : E) : x # y -> y # x
  | #-comparison (x y z : E) : x # z -> x # y || y # z

\class TightApartness \extends Apartness {
  | tightness (x y : E) : Not (x # y) -> x = y

  \lemma stability {x y : E} (~~x=y : Not (Not (x = y))) : x = y =>
    tightness x y (\lam x#y => ~~x=y (\lam x=y => #-irreflexive x (transport (x #) (inv x=y) x#y)))
}
