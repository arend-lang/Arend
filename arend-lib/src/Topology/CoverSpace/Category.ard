\import Category
\import Category.Meta
\import Category.Subcat
\import Equiv (Embedding, Retraction)
\import Logic
\import Logic.Meta
\import Paths
\import Paths.Meta
\import Set.Subset
\import Topology.CoverSpace

\instance PrecoverSpaceCat : Cat PrecoverSpace
  | Hom => CoverMap
  | id X => CoverMap.id
  | o => CoverMap.∘
  | id-left => idp
  | id-right => idp
  | o-assoc => idp
  | univalence => sip \lam c d => exts \lam C => ext
      (\lam Cc => cauchy-subset (func-cover {d} Cc) \lam (inP (V,CV,p)) => transportInv C p CV,
       \lam Cc => cauchy-subset (func-cover {c} Cc) \lam (inP (V,CV,p)) => transportInv C p CV)

\instance CoverSpaceCat : Cat CoverSpace
  | Hom => CoverMap
  | id X => CoverMap.id
  | o => CoverMap.∘
  | id-left => idp
  | id-right => idp
  | o-assoc => idp
  | univalence => sip \lam c d => exts \lam C => ext
      (\lam Cc => cauchy-subset (func-cover {d} Cc) \lam (inP (V,CV,p)) => transportInv C p CV,
       \lam Cc => cauchy-subset (func-cover {c} Cc) \lam (inP (V,CV,p)) => transportInv C p CV)