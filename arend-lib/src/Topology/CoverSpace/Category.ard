\import Category
\import Category.Meta
\import Category.Subcat
\import Equiv (Embedding, Retraction)
\import Logic
\import Logic.Meta
\import Paths
\import Paths.Meta
\import Set.Subset
\import Topology.CoverSpace

\instance CoverSpaceCat : Cat CoverSpace
  | Hom => CoverMap
  | id X => CoverMap.id
  | o => CoverMap.∘
  | id-left => idp
  | id-right => idp
  | o-assoc => idp
  | univalence => sip \lam c d => exts \lam C => ext
      (\lam Cc => cauchy-subset (func-cover {d} Cc) \lam (inP (V,CV,p)) => transportInv C p CV,
       \lam Cc => cauchy-subset (func-cover {c} Cc) \lam (inP (V,CV,p)) => transportInv C p CV)

\instance CompletelyStronglyRegularCoverSpaceCat : Cat CompletelyStronglyRegularCoverSpace
  => subCat {CoverSpaceCat} \new Embedding {CompletelyStronglyRegularCoverSpace} {CoverSpace} {
    | f A => A
    | isEmb A B => \new Retraction {
      | sec p => path \lam i => \new CompletelyStronglyRegularCoverSpace {
        | CoverSpace => p i
        | isCompletelyStronglyRegular => prop-dpi (\lam i => \Pi {C : Set (Set (p i))} -> isCauchy {p i} C -> isCauchy {p i} \lam V => ∃ (U : Set (p i)) (C U) (V StronglyRatherBelow.<=<c U)) isCompletelyStronglyRegular isCompletelyStronglyRegular i
      }
      | f_sec => idpe
    }
  }