\import Arith.Rat
\import Data.Bool
\import Order.Lattice
\import Order.PartialOrder
\import Order.StrictOrder
\import Topology.Locale

\func RealPres : FramePres (\Sigma Rat Rat) \cowith
  | conj a b => (a.1 ∨ b.1, a.2 ∧ b.2)
  | BasicCover => FramePres.Indexing {\Sigma (x : Fin 3) (\case x \return \Set0 \with {
    | 0 => \Sigma (p r s q : Rat) (p < r) (r < s) (s < q)
    | 1 => \Sigma (p r q s : Rat) (p < r) (r < q) (q < s)
    | 2 => \Sigma Rat Rat
  })} (\case \elim __ \with {
    | (0,(p,r,s,q,_,_,_)) => (\Sigma, (r,s), \lam _ => (p,q))
    | (1,(p,r,q,s,_,_,_)) => (Bool, (p,s), if __ (p,q) (r,s))
    | (2,(p,q)) => (\Sigma (r s : Rat) (p < r) (r < s) (s < q), (p,q), \lam t => (t.1,t.2))
  })

\func RealLocale => PresentedFrame RealPres