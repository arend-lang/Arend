\import Algebra.Group
\import Algebra.Meta
\import Arith.Rat
\import Arith.Real.InfReal
\import Arith.Real.LowerReal
\import Arith.Real.UpperReal
\import Logic
\import Topology.MetricSpace.ValuedMetricSpace
\import Topology.NormedAbGroup.Real

\class ExtendedPseudoMetricSpace \extends LinearlyValuedPseudoMetricSpace
  | V => RealMetricValueLinearOrder
  \where {
    \instance RealMetricValueLinearOrder : MetricValueLinearOrder InfReal
      | LinearlyBiorderedAbMonoid => InfRealAbMonoid
      | hasPositive => inP (1, InfRealAbMonoid.zro<ide)
      | +_<-dense (inP (c,a<c,c<b)) => \case U-rounded a<c \with {
        | inP (d,a<d,d<c) => inP (c - d, InfRealAbMonoid.<_L.2 linarith, \case L-rounded c<b \with {
          | inP (c',c'<b,c<c') => inP (c', InfRealAbMonoid.+_U_<= (c' - d) a<d linarith linarith, c'<b)
        })
      }

    \func LowerRealMetricValueOrder : MetricValueOrder LowerReal \cowith
      | BiorderedLatticeAbMonoid => LowerRealAbMonoid
      | hasPositive => inP (1, LowerRealAbMonoid.zro<ide)
      | +_<-dense (inP (c,c<b,a<=c)) => \case L-rounded c<b \with {
        | inP (c',c'<b,c<c') => inP (c' - c, LowerRealAbMonoid.lower_<_L.2 linarith, inP (c', c'<b, \lam {x} l => \case LowerRealAbMonoid.+_L.1 l \with {
          | inP (a',a'<a,d,d<c'-c,x<a'+d) => linarith (a<=c a'<a)
        }))
      }
  }
