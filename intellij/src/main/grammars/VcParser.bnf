{
    parserClass = "org.vclang.lang.core.parser.VcParser"

    implements = 'org.vclang.lang.core.psi.ext.VcCompositeElement'
    extends = 'org.vclang.lang.core.psi.ext.VcCompositeElementImpl'

    elementTypeHolderClass = "org.vclang.lang.core.psi.VcElementTypes"

    elementTypeClass = "org.vclang.lang.core.psi.VcCompositeElementType"
    tokenTypeClass = "org.vclang.lang.core.psi.VcTokenType"

    psiClassPrefix = "Vc"
    psiImplClassSuffix = "Impl"
    psiPackage = "org.vclang.lang.core.psi"
    psiImplPackage = "org.vclang.lang.core.psi.impl"

    generateTokenAccessors = true

    tokens = [
        LBRACE              = '{'
        RBRACE              = '}'
        LPAREN              = '('
        RPAREN              = ')'
        COLON               = ':'
        COLONCOLON          = '::'
        ARROW               = '->'
        FAT_ARROW           = '=>'
        DOT                 = '.'
        COMMA               = ','
        UNDERSCORE          = '_'
        GRAVE               = '`'
        PIPE                = '|'
        BLOCK_COMMENT_START = '{-'
        BLOCK_COMMENT_END   = '-}'

        OPEN_KW             = '\open'
        EXPORT_KW           = '\export'
        HIDING_KW           = '\hiding'
        FUNCTION_KW         = '\function'
        NON_ASSOC_KW        = '\infix'
        LEFT_ASSOC_KW       = '\infixl'
        RIGHT_ASSOC_KW      = '\infixr'
        PROP_KW             = '\Prop'
        WHERE_KW            = '\where'
        WITH_KW             = '\with'
        ELIM_KW             = '\elim'
        NEW_KW              = '\new'
        PI_KW               = '\Pi'
        SIGMA_KW            = '\Sigma'
        LAM_KW              = '\lam'
        LET_KW              = '\let'
        IN_KW               = '\in'
        CASE_KW             = '\case'
        DATA_KW             = '\data'
        CLASS_KW            = '\class'
        EXTENDS_KW          = '\extends'
        VIEW_KW             = '\view'
        ON_KW               = '\on'
        BY_KW               = '\by'
        INSTANCE_KW         = '\instance'
        TRUNCATED_KW        = '\truncated'
        DEFAULT_KW          = '\default'
        LP_KW               = '\lp'
        LH_KW               = '\lh'
        SUC_KW              = '\suc'
        MAX_KW              = '\max'

        LINE_COMMENT        = "regexp:--[^\r\n]*"
        BLOCK_COMMENT       = '{- BLOCK_COMMENT -}'
    ]

    extends("def(Function|Data|Class|ClassView|Instance)|class(Field|Implement|ViewField)|constructor") = definition
    extends("(arr|binOp|pi|sigma|lam|let|case)Expr") = expr
}

sourceNode ::= topStatement*

private topStatement ::= !<<eof>> statement { recoverWhile = statement_recover }

private statement_recover ::= !(OPEN_KW      | EXPORT_KW    | FUNCTION_KW |
                                TRUNCATED_KW | DATA_KW      | CLASS_KW    |
                                VIEW_KW      | DEFAULT_KW   | INSTANCE_KW)

statement ::= statCmd | statDef

statCmd ::= nsCmd nsCmdRoot fieldAcc* [HIDING_KW? LPAREN <<comma_separated_sequence refIdentifier>> RPAREN] {
    pin = 1
    mixin = "org.vclang.lang.core.psi.ext.VcStatCmdImplMixin"
}

nsCmd ::= OPEN_KW | EXPORT_KW

nsCmdRoot ::= moduleName | refIdentifier {
    mixin = "org.vclang.lang.core.psi.ext.VcNsCmdRootImplMixin"
}

statDef ::= definition

classStat ::= classField
    | classImplement
    | statement

classField ::= PIPE prec defIdentifier COLON expr {
    implements = [ "org.vclang.lang.core.psi.ext.VcNamedElement"
                   "com.jetbrains.jetpad.vclang.term.Abstract.ClassField" ]
    mixin = "org.vclang.lang.core.psi.ext.adapters.ClassFieldAdapter"
    stubClass = "org.vclang.lang.core.stubs.VcClassFieldStub"
    elementTypeFactory = "org.vclang.lang.core.stubs.StubImplementationsKt.factory"
}

classImplement ::= PIPE refIdentifier FAT_ARROW expr {
    implements = [ "org.vclang.lang.core.psi.ext.VcCompositeElement"
                   "com.jetbrains.jetpad.vclang.term.Abstract.Implementation" ]
    mixin = "org.vclang.lang.core.psi.ext.adapters.ClassImplementAdapter"
    stubClass = "org.vclang.lang.core.stubs.VcClassImplementStub"
    elementTypeFactory = "org.vclang.lang.core.stubs.StubImplementationsKt.factory"
}

definition ::= defFunction
    | defData
    | defClass
    | defClassView
    | defInstance {
    implements = [ "org.vclang.lang.core.psi.ext.VcNamedElement"
                   "com.jetbrains.jetpad.vclang.term.Abstract.Definition" ]
    mixin = "org.vclang.lang.core.psi.ext.adapters.DefinitionAdapter"
}

defFunction ::= FUNCTION_KW prec defIdentifier tele* [COLON expr] functionBody where? {
    pin = 1
    implements = [ "org.vclang.lang.core.Surrogate.StatementCollection"
                   "com.jetbrains.jetpad.vclang.term.Abstract.FunctionDefinition" ]
    mixin = "org.vclang.lang.core.psi.ext.adapters.FunctionDefinitionAdapter"
    stubClass = "org.vclang.lang.core.stubs.VcDefFunctionStub"
    elementTypeFactory = "org.vclang.lang.core.stubs.StubImplementationsKt.factory"
}

defData ::= TRUNCATED_KW? DATA_KW prec defIdentifier tele* [COLON expr] dataBody {
    pin = 2
    implements = [ "com.jetbrains.jetpad.vclang.term.Abstract.DataDefinition" ]
    mixin = "org.vclang.lang.core.psi.ext.adapters.DataDefinitionAdapter"
    stubClass = "org.vclang.lang.core.stubs.VcDefDataStub"
    elementTypeFactory = "org.vclang.lang.core.stubs.StubImplementationsKt.factory"
}

defClass ::= CLASS_KW defIdentifier classTeles [EXTENDS_KW <<comma_separated_sequence atomFieldsAcc>>] classStats where? {
    pin = 1
    implements = [ "org.vclang.lang.core.Surrogate.StatementCollection"
                   "com.jetbrains.jetpad.vclang.term.Abstract.ClassDefinition" ]
    mixin = "org.vclang.lang.core.psi.ext.adapters.ClassDefinitionAdapter"
    stubClass = "org.vclang.lang.core.stubs.VcDefClassStub"
    elementTypeFactory = "org.vclang.lang.core.stubs.StubImplementationsKt.factory"
}

classTeles ::= tele*

classStats ::= [LBRACE classStat* RBRACE]

defClassView ::= VIEW_KW defIdentifier ON_KW expr BY_KW refIdentifier LBRACE classViewField* RBRACE {
    pin = 1
    implements = [ "com.jetbrains.jetpad.vclang.term.Abstract.ClassView" ]
    mixin = "org.vclang.lang.core.psi.ext.adapters.ClassViewAdapter"
    stubClass = "org.vclang.lang.core.stubs.VcDefClassViewStub"
    elementTypeFactory = "org.vclang.lang.core.stubs.StubImplementationsKt.factory"
}

defInstance ::= DEFAULT_KW? INSTANCE_KW defIdentifier tele* FAT_ARROW expr {
    pin = 2
    implements = [ "com.jetbrains.jetpad.vclang.term.Abstract.ClassViewInstance" ]
    mixin = "org.vclang.lang.core.psi.ext.adapters.ClassViewInstanceAdapter"
    stubClass = "org.vclang.lang.core.stubs.VcDefInstanceStub"
    elementTypeFactory = "org.vclang.lang.core.stubs.StubImplementationsKt.factory"
}

functionBody ::= withoutElim | withElim
withoutElim ::= FAT_ARROW !ELIM_KW expr
withElim ::= elim? clauses

dataBody ::= dataClauses | dataConstructors
dataClauses ::= elim constructorClause*
dataConstructors ::= [FAT_ARROW PIPE? constructor] (PIPE constructor)*

constructorClause ::= PIPE <<comma_separated_sequence pattern>> FAT_ARROW (constructor | LBRACE <<pipe_separated_sequence constructor>> RBRACE)

elim ::= WITH_KW | FAT_ARROW ELIM_KW <<comma_separated_sequence atomFieldsAcc>>

classViewField ::= defIdentifier [FAT_ARROW prec refIdentifier] {
    implements = [ "com.jetbrains.jetpad.vclang.term.Abstract.ClassViewField" ]
    mixin = "org.vclang.lang.core.psi.ext.adapters.ClassViewFieldAdapter"
    stubClass = "org.vclang.lang.core.stubs.VcClassViewFieldStub"
    elementTypeFactory = "org.vclang.lang.core.stubs.StubImplementationsKt.factory"
}

where ::= WHERE_KW (LBRACE statement* RBRACE | statement) { pin = 1 }

pattern ::= atomPattern | patternConstructor {
    mixin = "org.vclang.lang.core.psi.ext.VcPatternImplMixin"
}

patternConstructor ::= prefixName atomPatternOrPrefix*

atomPatternOrPrefix ::= atomPattern | prefixName

atomPattern ::= LPAREN pattern? RPAREN
    | LBRACE pattern RBRACE
    | UNDERSCORE

constructor ::= prec defIdentifier tele* [elim? clausesWithBraces] {
    implements = [ "com.jetbrains.jetpad.vclang.term.Abstract.Constructor" ]
    mixin = "org.vclang.lang.core.psi.ext.adapters.ConstructorAdapter"
    stubClass = "org.vclang.lang.core.stubs.VcConstructorStub"
    elementTypeFactory = "org.vclang.lang.core.stubs.StubImplementationsKt.factory"
}

prec ::= [associativity NUMBER]

associativity ::= NON_ASSOC_KW
    | LEFT_ASSOC_KW
    | RIGHT_ASSOC_KW

expr0 ::= binOpLeft* binOpArg postfixName*

expr ::= arrExpr
    | caseExpr
    | sigmaExpr
    | piExpr
    | lamExpr
    | letExpr
    | binOpExpr

arrExpr ::= expr ARROW expr { rightAssociative = true }

binOpExpr ::= binOpLeft* newExpr

caseExpr ::= CASE_KW <<comma_separated_sequence expr0>> WITH_KW clausesWithBraces { pin = 1 }

piExpr ::= PI_KW tele+ ARROW expr {
    pin = 1
    mixin = "org.vclang.lang.core.psi.ext.VcPiExprImplMixin"
}

sigmaExpr ::= SIGMA_KW tele+ {
    pin = 1
    mixin = "org.vclang.lang.core.psi.ext.VcSigmaExprImplMixin"
}

lamExpr ::= LAM_KW tele+ FAT_ARROW expr {
    pin = 1
    mixin = "org.vclang.lang.core.psi.ext.VcLamExprImplMixin"
}

letExpr ::= LET_KW <<pipe_separated_sequence letClause>> IN_KW expr {
    pin = 1
    mixin = "org.vclang.lang.core.psi.ext.VcLetExprImplMixin"
}

clauses ::= clausesWithBraces | (PIPE clause)*
private clausesWithBraces ::= LBRACE <<pipe_separated_sequence clause>>? RBRACE

letClause ::= defIdentifier tele* typeAnnotation? FAT_ARROW expr {
    implements = [ "org.vclang.lang.core.psi.ext.VcNamedElement" ]
    mixin = "org.vclang.lang.core.psi.ext.VcNamedElementImpl"
}

typeAnnotation ::= COLON expr { pin = 1 }

clause ::= <<comma_separated_sequence pattern>> [FAT_ARROW expr]

levelExpr ::= atomLevelExpr
    | sucLevelExpr
    | maxLevelExpr

atomLevelExpr ::= LP_KW
    | LH_KW
    | NUMBER
    | LPAREN levelExpr RPAREN

sucLevelExpr ::= SUC_KW atomLevelExpr { pin = 1 }

maxLevelExpr ::= MAX_KW atomLevelExpr atomLevelExpr { pin = 1 }

binOpArg ::= universeBinOp
    | setUniverseBinOp
    | truncatedUniverseBinOp
    | argumentBinOp

universeBinOp ::= UNIVERSE [atomLevelExpr atomLevelExpr?] { pin = 1 }

setUniverseBinOp ::= SET atomLevelExpr? { pin = 1 }

truncatedUniverseBinOp ::= TRUNCATED_UNIVERSE atomLevelExpr? { pin = 1 }

argumentBinOp ::= atomFieldsAcc argument*

binOpLeft ::= newExpr infixName

newExpr ::= NEW_KW? binOpArg implementStatements? postfixName* {
    pin = 1
    mixin = "org.vclang.lang.core.psi.ext.VcNewExprImplMixin"
}

fieldAcc ::= DOT (NUMBER | refIdentifier) {
    pin = 1
    implements = [ "org.vclang.lang.core.psi.ext.VcReferenceElement" ]
    mixin = "org.vclang.lang.core.psi.ext.VcFieldAccImplMixin"
}

moduleName ::= (COLONCOLON moduleNamePart)+

moduleNamePart ::= refIdentifier {
    implements = [ "org.vclang.lang.core.psi.ext.VcReferenceElement" ]
    mixin = "org.vclang.lang.core.psi.ext.VcModuleNamePartImplMixin"
}

atom ::= literal
    | tuple
    | NUMBER
    | atomModuleCall

tuple ::= LPAREN <<comma_separated_sequence expr>> RPAREN

atomModuleCall ::= moduleName

atomFieldsAcc ::= atom fieldAcc*

implementStatements ::= LBRACE <<pipe_separated_sequence implementStatement>>? RBRACE

implementStatement ::= refIdentifier FAT_ARROW expr

argument ::= atomFieldsAcc
    | universeAtom
    | LBRACE expr RBRACE

literal ::= prefixName
    | PROP_KW
    | UNDERSCORE
    | goal

goal ::= LBRACE '?' defIdentifier? [LBRACE expr RBRACE] RBRACE {
    pin = 2
    implements = [ "org.vclang.lang.core.psi.ext.VcNamedElement" ]
    mixin = "org.vclang.lang.core.psi.ext.VcNamedElementImpl"
}

universeAtom ::= SET
    | UNIVERSE
    | TRUNCATED_UNIVERSE

tele ::= literal
    | universeAtom
    | LPAREN typedExpr RPAREN
    | LBRACE typedExpr RBRACE

typedExpr ::= identifierOrUnknown+ COLON expr | expr  // expr [COLON expr]

identifierOrUnknown ::= defIdentifier | UNDERSCORE

defIdentifier ::= PREFIX | INFIX {
    implements = [ "org.vclang.lang.core.psi.ext.VcReferenceElement" ]
    mixin = "org.vclang.lang.core.psi.ext.VcDefIdentifierImplMixin"
}

refIdentifier ::= PREFIX | INFIX {
    implements = [ "org.vclang.lang.core.psi.ext.VcReferenceElement" ]
    mixin = "org.vclang.lang.core.psi.ext.VcRefIdentifierImplMixin"
}

prefixName ::= PREFIX | PREFIX_INFIX {
    implements = [ "org.vclang.lang.core.psi.ext.VcReferenceElement" ]
    mixin = "org.vclang.lang.core.psi.ext.VcPrefixImplMixin"
}

infixName ::= INFIX | INFIX_PREFIX {
    implements = [ "org.vclang.lang.core.psi.ext.VcReferenceElement" ]
    mixin = "org.vclang.lang.core.psi.ext.VcInfixImplMixin"
}

postfixName ::= POSTFIX_INFIX | POSTFIX_PREFIX {
    implements = [ "org.vclang.lang.core.psi.ext.VcReferenceElement" ]
    mixin = "org.vclang.lang.core.psi.ext.VcPostfixImplMixin"
}

private meta comma_separated_sequence ::= <<p>> (COMMA <<p>>)* { pin(".*") = 1 }

private meta pipe_separated_sequence ::= PIPE? <<p>> (PIPE <<p>>)* { pin(".*") = 1 }
