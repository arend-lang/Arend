\data Nat
  | zero
  | suc Nat
  \where {
    \func \infixl 6 + (x y : Nat) : Nat \elim y
      | zero => x
      | suc y => suc (x + y)

    \func \infixl 7 * (x y : Nat) : Nat \elim y
      | zero => zero
      | suc y => x * y + x
  }

\data Int
  | pos Nat
  | neg Nat \with { zero => pos zero }
  \where {
    \use \coerce fromNat (n : Nat) => pos n
  }

\data I
  | left
  | right

\data Path (A : I -> \Type) (a : A left) (a' : A right)
  | path (\Pi (i : I) -> A i)
  \where {
    \func inProp {A : \Prop} : \Pi (a a' : A) -> a = a'
  }

\func \infix 1 = {A : \Type} (a a' : A) => Path (\lam _ => A) a a'

\func \infixl 9 @ {A : I -> \Type} {a : A left} {a' : A right} (p : Path A a a') (i : I) : A i \elim p, i
  | _, left => a
  | _, right => a'
  | path f, i => f i

\func coe (A : I -> \Type) (a : A left) (i : I) : A i \elim i
  | left => a

\func iso {A B : \Type} (f : A -> B) (g : B -> A) (p : \Pi (x : A) -> g (f x) = x) (q : \Pi (y : B) -> f (g y) = y) (i : I) : \Type \elim i
  | left => A
  | right => B

\data TrP (A : \Type)
  | inP A
  | truncP (a a' : TrP A) (i : I) \elim i {
    | left  => a
    | right => a'
  } \where {
    \use \level isProp {A : \Type} (a a' : TrP A) : a = a' => path (truncP a a')
  }

\data TrS (A : \Type)
  | inS A
  | truncS {a a' : TrS A} (p p' : a = a') (i j : I) \elim i, j {
    | i, left  => p @ i
    | i, right => p' @ i
    | left,  _ => a
    | right, _ => a'
  } \where {
    \use \level isSet {A : \Type} (a a' : TrS) (p p' : a = a') : p = p' => path (\lam i => path (truncS p p' i))
  }
